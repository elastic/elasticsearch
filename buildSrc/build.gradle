/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
import java.nio.file.Files
import org.gradle.util.GradleVersion

import java.util.regex.Matcher
import java.util.regex.Pattern

plugins {
  id 'java-gradle-plugin'
  id 'groovy'
}

group = 'org.elasticsearch.gradle'

String minimumGradleVersion = file('src/main/resources/minimumGradleVersion').text.trim()
if (GradleVersion.current() < GradleVersion.version(minimumGradleVersion)) {
    throw new GradleException("Gradle ${minimumGradleVersion}+ is required to build elasticsearch")
}

if (project == rootProject) {
  // change the build dir used during build init, so that doing a clean
  // won't wipe out the buildscript jar
  buildDir = 'build-bootstrap'
}

/*****************************************************************************
 *         Propagating version.properties to the rest of the build           *
 *****************************************************************************/

Properties props = new Properties()
props.load(project.file('version.properties').newDataInputStream())
version = props.getProperty('elasticsearch')
boolean snapshot = "true".equals(System.getProperty("build.snapshot", "true"));
if (snapshot) {
  // we update the version property to reflect if we are building a snapshot or a release build
  // we write this back out below to load it in the Build.java which will be shown in rest main action
  // to indicate this being a snapshot build or a release build.
  version += "-SNAPSHOT"
  props.put("elasticsearch", version);
}

File tempPropertiesFile = new File(project.buildDir, "version.properties")
task writeVersionProperties {
  inputs.properties(props)
  outputs.file(tempPropertiesFile)
  doLast {
    OutputStream stream = Files.newOutputStream(tempPropertiesFile.toPath());
    try {
      props.store(stream, "UTF-8");
    } finally {
      stream.close();
    }
  }
}

processResources {
  dependsOn writeVersionProperties
  from tempPropertiesFile
}


if (JavaVersion.current() < JavaVersion.VERSION_1_10) {
  throw new GradleException('At least Java 10 is required to build elasticsearch gradle tools')
}

/*****************************************************************************
 *         Java version                                                      *
 *****************************************************************************/

// Gradle 4.10 does not support setting this to 11 yet
targetCompatibility = "10"
sourceCompatibility = "10"

// We have a few classes that need to be compiled for older java versions because these are used to run checks against
// those
sourceSets {
  minimumRuntime {
    // We only want Java here, but the Groovy doesn't configure javadoc correctly if we don't define this as groovy
    groovy {
      srcDirs = ['src/main/minimumRuntime']
    }
  }
}
compileMinimumRuntimeGroovy {
  // We can't use BuildPlugin here, so read from file
  String minimumRuntimeVersion = file('src/main/resources/minimumRuntimeVersion').text.trim()
  targetCompatibility = minimumRuntimeVersion
  sourceCompatibility = minimumRuntimeVersion
}
dependencies {
  compile sourceSets.minimumRuntime.output
  minimumRuntimeCompile "junit:junit:${props.getProperty('junit')}"
  minimumRuntimeCompile localGroovy()
}
jar {
  from sourceSets.minimumRuntime.output
}


/*****************************************************************************
 *                    Dependencies used by the entire build                  *
 *****************************************************************************/

repositories {
  jcenter()
}

dependencies {
  compile localGroovy()
  compile "com.carrotsearch.randomizedtesting:junit4-ant:${props.getProperty('randomizedrunner')}"
  compile "com.carrotsearch.randomizedtesting:randomizedtesting-runner:${props.getProperty('randomizedrunner')}"
  
  compile 'com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3'
  compile 'com.netflix.nebula:nebula-publishing-plugin:4.4.4'
  compile 'com.netflix.nebula:gradle-info-plugin:3.0.3'
  compile 'org.eclipse.jgit:org.eclipse.jgit:3.2.0.201312181205-r'
  compile 'com.perforce:p4java:2012.3.551082' // THIS IS SUPPOSED TO BE OPTIONAL IN THE FUTURE....
  compile 'org.apache.rat:apache-rat:0.11'
  compile "org.elasticsearch:jna:4.5.1"
  compile 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
  compile 'de.thetaphi:forbiddenapis:2.6'
  testCompile "junit:junit:${props.getProperty('junit')}"
}


// Gradle 2.14+ removed ProgressLogger(-Factory) classes from the public APIs
// Use logging dependency instead
// Gradle 4.3.1 stopped releasing the logging jars to jcenter, just use the last available one
GradleVersion logVersion = GradleVersion.current() > GradleVersion.version('4.3') ? GradleVersion.version('4.3') : GradleVersion.current()

dependencies {
  compileOnly "org.gradle:gradle-logging:${logVersion.getVersion()}"
}

/*****************************************************************************
 *                         Bootstrap repositories                            *
 *****************************************************************************/
// this will only happen when buildSrc is built on its own during build init
if (project == rootProject) {
  repositories {
    if (System.getProperty("repos.mavenLocal") != null) {
      mavenLocal()
    }
  }
  test {
    include "**/*Tests.class"
    exclude "**/*IT.class"
  }
}

/*****************************************************************************
 *                           Normal project checks                           *
 *****************************************************************************/

// this happens when included as a normal project in the build, which we do
// to enforce precommit checks like forbidden apis, as well as setup publishing
if (project != rootProject) {
  apply plugin: 'elasticsearch.build'
  apply plugin: 'nebula.maven-base-publish'
  apply plugin: 'nebula.maven-scm'

  // groovydoc succeeds, but has some weird internal exception...
  groovydoc.enabled = false

  // build-tools is not ready for primetime with these...
  dependencyLicenses.enabled = false
  dependenciesInfo.enabled = false
  forbiddenApisMain.enabled = false
  forbiddenApisTest.enabled = false
  forbiddenApisMinimumRuntime.enabled = false
  jarHell.enabled = false
  thirdPartyAudit.enabled = false

  // tests can't  be run with randomized test runner
  // it's fine as we run them as part of :buildSrc
  test.enabled = false
  task integTest(type: Test) {
    // integration test requires the local testing repo for example plugin builds
    dependsOn project.rootProject.allprojects.collect {
      it.tasks.matching { it.name == 'publishNebulaPublicationToLocalTestRepository'}
    }
    exclude "**/*Tests.class"
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    inputs.dir(file("src/testKit"))
    // tell BuildExamplePluginsIT where to find the example plugins
    systemProperty (
            'test.build-tools.plugin.examples',
            files(
                    project(':example-plugins').subprojects.collect { it.projectDir }
            ).asPath,
    )
    systemProperty 'test.local-test-repo-path', "${rootProject.buildDir}/local-test-repo"
    Matcher isLuceneSnapshot = (/\w+-snapshot-([a-z0-9]+)/ =~ versions.lucene)
    if (isLuceneSnapshot) {
      systemProperty 'test.lucene-snapshot-revision', isLuceneSnapshot[0][1]
    }
    systemProperty 'test.version_under_test', version
  }
  check.dependsOn(integTest)

  // TODO: re-enable once randomizedtesting gradle code is published and removed from here
  licenseHeaders.enabled = false

  forbiddenPatterns {
    exclude '**/*.wav'
    // the file that actually defines nocommit
    exclude '**/ForbiddenPatternsTask.groovy'
  }

  namingConventions {
    testClass = 'org.elasticsearch.gradle.test.GradleUnitTestCase'
    integTestClass = 'org.elasticsearch.gradle.test.GradleIntegrationTestCase'
  }

  /*
   * We alread configure publication and we don't need or want this one that
   * comes from the java-gradle-plugin.
   */
  afterEvaluate {
    generatePomFileForPluginMavenPublication.enabled = false
  }
}

plugins {
    id 'elasticsearch.testclusters'
    id 'base'
}

allprojects { all ->
    repositories {
        flatDir {
            dir System.getProperty("test.local-test-downloads-path")
        }
        maven {
            name "local"
            url System.getProperty("local.repo.path")
        }
        String luceneSnapshotRevision = System.getProperty("test.lucene-snapshot-revision")
        if (luceneSnapshotRevision != null) {
            maven {
                name "lucene-snapshots"
                url "https://s3.amazonaws.com/download.elasticsearch.org/lucenesnapshots/" + luceneSnapshotRevision
            }
        }
    }

    if (project == rootProject || project.name == "alpha" || project.name == "bravo") {
        apply plugin: 'elasticsearch.testclusters'

        all.testClusters {
            myTestCluster {
                testDistribution = 'DEFAULT'
                version = System.getProperty("test.version_under_test")
                javaHome = file(System.getProperty('java.home'))
                plugin file("${project(":dummyPlugin").buildDir}/distributions/dummy-${System.getProperty("test.version_under_test")}.zip")
            }
        }

        task user1 {
            useCluster testClusters.myTestCluster
            doFirst {
                println "$path: Cluster running @ ${testClusters.myTestCluster.httpSocketURI}"
            }
        }
        task user2 {
            useCluster testClusters.myTestCluster
            doFirst {
                println "$path: Cluster running @ ${testClusters.myTestCluster.httpSocketURI}"
            }
        }
        syncTestClustersArtifacts {
            dependsOn ":dummyPlugin:bundlePlugin"
        }
    }
}

testClusters {
    multiNode {
        version = System.getProperty("test.version_under_test")
        testDistribution = 'DEFAULT'
        javaHome = file(System.getProperty('java.home'))
        numberOfNodes = 3
    }
    releasedVersionDefault {
        version = "7.0.0"
        testDistribution = 'DEFAULT'
        javaHome = file(System.getProperty('java.home'))
    }
    releasedVersionOSS {
        version = "7.0.0"
        testDistribution = 'OSS'
        javaHome = file(System.getProperty('java.home'))
    }
    releasedVersionIntegTest {
        version = "7.0.0"
        testDistribution = 'INTEG_TEST'
        javaHome = file(System.getProperty('java.home'))
    }
}

task multiNode {
    useCluster testClusters.multiNode
    doFirst {
        println "$path: Cluster running @ ${testClusters.multiNode.httpSocketURI}"
    }
}

task testReleased {
    useCluster testClusters.releasedVersionDefault
    useCluster testClusters.releasedVersionOSS
    useCluster testClusters.releasedVersionIntegTest
    doFirst {
        println "$path: Cluster running @ ${testClusters.releasedVersionDefault.httpSocketURI}"
        println "$path: Cluster running @ ${testClusters.releasedVersionOSS.httpSocketURI}"
        println "$path: Cluster running @ ${testClusters.releasedVersionIntegTest.httpSocketURI}"
    }
}

task printLog {
    useCluster testClusters.myTestCluster
    doFirst {
        println "$path: Cluster running @ ${testClusters.myTestCluster.httpSocketURI}"
        testClusters.myTestCluster.singleNode().logLines().each {
            println it
        }
    }
}

task upToDate1 {
    useCluster testClusters.myTestCluster
    outputs.upToDateWhen { true }
    doLast {
        println "Some task action"
    }
}

task skipped1 {
    enabled = false
    useCluster testClusters.myTestCluster
}

task skipped2 {
    enabled = false
    useCluster testClusters.myTestCluster
}

task itAlwaysFails {
    doLast {
        throw new GradleException("Task 1 failed!")
    }
    useCluster testClusters.myTestCluster
}

task dependsOnFailed {
    dependsOn itAlwaysFails
    useCluster testClusters.myTestCluster
}

task illegalConfigAlter {
    useCluster testClusters.myTestCluster
    doFirst {
        println "Going to alter configuration after use"
        testClusters.myTestCluster.testDistribution = 'OSS'
    }
}

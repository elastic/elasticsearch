import org.elasticsearch.gradle.precommit.ThirdPartyAuditTask

plugins {
  id 'java'
  // bring in build-tools onto the classpath
  id 'elasticsearch.global-build-info' apply false
}

repositories {
  /**
   * Local test repo contains dummy jars with different group names and versions.
   *   - broken-log4j creates a log4j logger but has no pom, so the class will be missing
   *   - dummy-io has a class that creates a new java.io.File ( something which third-party-audit-absurd.txt forbids )
   *   - version 0.0.2 has the same class and one extra file just to make the jar different
   */
  maven {
    name = "local-test"
    url = file("sample_jars/build/testrepo")
    metadataSources {
      artifact()
    }
  }
  jcenter()
}

configurations.register("forbiddenApisCliJar")

dependencies {
  forbiddenApisCliJar 'de.thetaphi:forbiddenapis:2.7'
  compileOnly "org.${project.properties.compileOnlyGroup}:${project.properties.compileOnlyVersion}"
  compile "org.${project.properties.compileGroup}:${project.properties.compileVersion}"
}

tasks.register("empty", ThirdPartyAuditTask) {
  targetCompatibility = JavaVersion.VERSION_11
  signatureFile = file('third-party-audit-empty.txt')
}

tasks.register("absurd", ThirdPartyAuditTask) {
  targetCompatibility = JavaVersion.VERSION_11
  signatureFile = file('third-party-audit-absurd.txt')
}

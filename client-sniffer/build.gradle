/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.elasticsearch.gradle.precommit.PrecommitTasks
import org.gradle.api.JavaVersion

apply plugin: 'elasticsearch.build'
apply plugin: 'ru.vyarus.animalsniffer'

targetCompatibility = JavaVersion.VERSION_1_7
sourceCompatibility = JavaVersion.VERSION_1_7

dependencies {
  compile "org.elasticsearch:client:${version}"
  compile "org.apache.httpcomponents:httpclient:${versions.httpclient}"
  compile "org.apache.httpcomponents:httpcore:${versions.httpcore}"
  compile "commons-codec:commons-codec:${versions.commonscodec}"
  compile "commons-logging:commons-logging:${versions.commonslogging}"
  compile "com.fasterxml.jackson.core:jackson-core:${versions.jackson}"

  testCompile "com.carrotsearch.randomizedtesting:randomizedtesting-runner:${versions.randomizedrunner}"
  testCompile "junit:junit:${versions.junit}"
  testCompile "org.hamcrest:hamcrest-all:${versions.hamcrest}"
  testCompile "org.apache.lucene:lucene-test-framework:${versions.lucene}"
  testCompile "org.apache.lucene:lucene-core:${versions.lucene}"
  testCompile "org.apache.lucene:lucene-codecs:${versions.lucene}"
  testCompile "org.elasticsearch:securemock:${versions.securemock}"
  testCompile "org.codehaus.mojo:animal-sniffer-annotations:1.15"
  signature "org.codehaus.mojo.signature:java17:1.0@signature"
}

compileJava.options.compilerArgs << '-target' << '1.7' << '-source' << '1.7' << '-Xlint:all,-path,-serial,-options'
compileTestJava.options.compilerArgs << '-target' << '1.7' << '-source' << '1.7'

forbiddenApisMain {
  //client does not depend on core, so only jdk signatures should be checked
  signaturesURLs = [PrecommitTasks.getResource('/forbidden/jdk-signatures.txt')]
}

forbiddenApisTest {
  //we are excluding jdk-non-portable to allow for com.sun.net.httpserver.* usage
  //TODO remove this line once https://github.com/policeman-tools/forbidden-apis/issues/103 gets solved
  bundledSignatures = ['jdk-unsafe', 'jdk-deprecated', 'jdk-system-out']
  //client does not depend on core, so only jdk signatures should be checked
  signaturesURLs = [PrecommitTasks.getResource('/forbidden/jdk-signatures.txt')]
}

//JarHell is part of es core, which we don't want to pull in
jarHell.enabled=false
//NamingConventionCheck is part of test-framework, which we don't want to pull in as it depends on es core
namingConventions.enabled=false

dependencyLicenses {
  dependencies = project.configurations.runtime.fileCollection {
    it.group.startsWith('org.elasticsearch') == false
  }
}

thirdPartyAudit.excludes = [
  //commons-logging optional dependencies
  'org.apache.avalon.framework.logger.Logger',
  'org.apache.log.Hierarchy',
  'org.apache.log.Logger',
  'org.apache.log4j.Category',
  'org.apache.log4j.Level',
  'org.apache.log4j.Logger',
  'org.apache.log4j.Priority',
  //commons-logging provided dependencies
  'javax.servlet.ServletContextEvent',
  'javax.servlet.ServletContextListener'
]

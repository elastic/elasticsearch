
apply plugin: 'org.elasticsearch.build'

jar {
  baseName = 'elasticsearch'
}

configurations {
  optional // these are used at compile time (and test) but excluded from runtime
  restSpec // the rest spec is copied independently to the test classes dir
}

versions << [
  jackson:  '2.5.3',
  log4j:    '1.2.17',
  slf4j:    '1.6.2' 
]

dependencies {

  // lucene
  compile "org.apache.lucene:lucene-core:${versions.lucene}"
  compile "org.apache.lucene:lucene-backward-codecs:${versions.lucene}"
  compile "org.apache.lucene:lucene-analyzers-common:${versions.lucene}"
  compile "org.apache.lucene:lucene-queries:${versions.lucene}"
  compile "org.apache.lucene:lucene-memory:${versions.lucene}"
  compile "org.apache.lucene:lucene-highlighter:${versions.lucene}"
  compile "org.apache.lucene:lucene-queryparser:${versions.lucene}"
  compile "org.apache.lucene:lucene-suggest:${versions.lucene}"
  compile "org.apache.lucene:lucene-join:${versions.lucene}"
  compile "org.apache.lucene:lucene-spatial:${versions.lucene}"

  // lucene spatial
  optional 'com.spatial4j:spatial4j:0.4.1'
  optional 'com.vividsolutions:jts:1.13'

  compile 'com.google.guava:guava:18.0'
  compile 'commons-cli:commons-cli:1.3.1'
  compile 'com.carrotsearch:hppc:0.7.1'
  optional 'net.java.dev.jna:jna:4.1.0'

  // time handling, remove with java 8 time
  compile 'joda-time:joda-time:2.8.2'
  // joda 2.0 moved to using volatile fields for datetime
  // When updating to a new version, make sure to update our copy of BaseDateTime
  compile 'org.joda:joda-convert:1.2'

  // json and yaml
  compile "com.fasterxml.jackson.core:jackson-core:${versions.jackson}"
  compile "com.fasterxml.jackson.dataformat:jackson-dataformat-smile:${versions.jackson}"
  compile(group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: versions.jackson) {
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
  }
  compile "com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:${versions.jackson}"

  compile 'io.netty:netty:3.10.3.Final'
  compile 'com.ning:compress-lzf:1.0.2'
  compile 'com.tdunning:t-digest:3.0'
  compile 'org.hdrhistogram:HdrHistogram:2.1.6'

  // scripting
  optional 'org.codehaus.groovy:groovy-all:2.4.4:indy'
  optional "org.apache.lucene:lucene-expressions:${versions.lucene}"
  // templating
  optional('com.github.spullara.mustache.java:compiler:0.8.13') {
    transitive = false
  }

  // logging
  optional "log4j:log4j:${versions.log4j}"
  optional "log4j:apache-log4j-extras:${versions.log4j}"
  optional "org.slf4j:slf4j-api:${versions.slf4j}"

  // TODO: remove these test deps and just dpeend on test-framework
  testCompile(group: 'junit', name: 'junit', version: '4.11') {
    transitive = false
  }
  //testCompile group: 'com.carrotsearch.randomizedtesting', name: 'randomizedtesting-runner', version: '2.1.16'

  testCompile "org.apache.lucene:lucene-test-framework:${versions.lucene}"
  testCompile(group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3') {
    exclude group: 'org.hamcrest', module: 'hamcrest-core'
  }
  testCompile 'com.google.jimfs:jimfs:1.0'
  testCompile 'org.apache.httpcomponents:httpclient:4.3.6'

  testCompile configurations.optional // optional deps are pulled into tests

  restSpec project(':rest-api-spec') // for rest tests
}

// add optional dependencies to compile classpath only
sourceSets.main.compileClasspath += [configurations.optional]

compileJava.options.compilerArgs << "-Xlint:-cast,-deprecation,-fallthrough,-overrides,-rawtypes,-serial,-try,-unchecked"
compileTestJava.options.compilerArgs << "-Xlint:-cast,-deprecation,-fallthrough,-overrides,-rawtypes,-serial,-try,-unchecked"

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java
}

artifacts {
    archives sourcesJar
}

forbiddenPatterns {
  exclude '**/*.json'
  exclude '**/*.jmx'
  exclude '**/org/elasticsearch/cluster/routing/shard_routes.txt'
}

task copyRestSpec(type: Copy, dependsOn: configurations.restSpec.buildDependencies) {
  from zipTree(configurations.restSpec.asPath)
  into sourceSets.test.output.resourcesDir
}

test {
  dependsOn copyRestSpec
}



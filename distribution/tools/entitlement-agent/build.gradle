/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the "Elastic License
 * 2.0", the "GNU Affero General Public License v3.0 only", and the "Server Side
 * Public License v 1"; you may not use this file except in compliance with, at
 * your election, the "Elastic License 2.0", the "GNU Affero General Public
 * License v3.0 only", or the "Server Side Public License, v 1".
 */

import static java.util.stream.Collectors.joining

apply plugin: 'elasticsearch.build'
apply plugin: 'elasticsearch.embedded-providers'

embeddedProviders {
  impl 'entitlement-agent', project(':distribution:tools:entitlement-agent:impl')
}

configurations {
  entitlementBridge
}

dependencies {
  entitlementBridge project(":distribution:tools:entitlement-bridge")
  compileOnly project(":libs:elasticsearch-core")
  compileOnly project(":distribution:tools:entitlement-runtime")
  testImplementation project(":test:framework")
  testImplementation project(":distribution:tools:entitlement-bridge")
  testImplementation project(":distribution:tools:entitlement-agent:impl")
}

tasks.named('test').configure {
  systemProperty "tests.security.manager", "false"
  dependsOn('jar')

  // Register an argument provider to avoid eager resolution of configurations
  jvmArgumentProviders.add(new CommandLineArgumentProvider() {
    @Override
    Iterable<String> asArguments() {
      return ["-javaagent:${tasks.jar.archiveFile.get()}", "-Des.entitlements.bridgeJar=${configurations.entitlementBridge.singleFile}"]
    }
  })


  // The Elasticsearch build plugin automatically adds all compileOnly deps as testImplementation.
  // We must not add the bridge this way because it is also on the boot classpath, and that would lead to jar hell.
  classpath -= files(configurations.entitlementBridge)
}

tasks.named('jar').configure {
  manifest {
    attributes(
      'Premain-Class': 'org.elasticsearch.entitlement.agent.EntitlementAgent'
      , 'Can-Retransform-Classes': 'true'
    )
  }
}

tasks.named('forbiddenApisMain').configure {
  replaceSignatureFiles 'jdk-signatures'
}


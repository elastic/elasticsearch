apply plugin: 'elasticsearch.build'

sourceSets {
  unsupportedJdkVersionEntrypoint
}

tasks.named(sourceSets.unsupportedJdkVersionEntrypoint.compileJavaTaskName).configure {
  targetCompatibility = JavaVersion.VERSION_1_8
}


tasks.named("jar") {
  manifest {
    attributes("Multi-Release": "true")
  }

  FileCollection mainOutput = sourceSets.main.output;
  from(sourceSets.unsupportedJdkVersionEntrypoint.output)
  eachFile { details ->
    if (details.path.equals("org/elasticsearch/tools/java_version_checker/JavaVersionChecker.class") &&
      mainOutput.asFileTree.contains(details.file)) {
      details.relativePath = details.relativePath.prepend("META-INF/versions/17")
    }
  }
}

// TODO revisit forbiddenApis issues
["javadoc", "forbiddenApisMain", "forbiddenApisUnsupportedJdkVersionEntrypoint"].each {
  tasks.named(it).configure { enabled = false }
}

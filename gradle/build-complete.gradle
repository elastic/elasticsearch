import java.nio.file.Files

String buildNumber = System.getenv('BUILD_NUMBER')

if (buildNumber) {
    File uploadFile = file("build/${buildNumber}.tar.bz2")
    project.gradle.buildFinished { result ->
        println "build complete, generating: $uploadFile"
        if (uploadFile.exists()) {
            project.delete(uploadFile)
        }

        try {
            ant.tar(destfile: uploadFile, compression: "bzip2", longfile: "gnu") {
                fileset(dir: projectDir) {
                    fileTree(projectDir)
                            .include("**/*.hprof")
                            .include("**/reaper.log")
                            .include("**/build/testclusters/**/logs/**")
                            .filter { Files.isRegularFile(it.toPath()) }
                            .each {
                                include(name: projectDir.toPath().relativize(it.toPath()))
                            }
                }

                fileset(dir: "${gradle.gradleUserHomeDir}/daemon/${gradle.gradleVersion}", followsymlinks: false) {
                    include(name: "**/daemon-${ProcessHandle.current().pid()}*.log")
                }
            }
        } catch (Exception e) {
            logger.lifecycle("Failed to archive additional logs", e)
        }
    }
}

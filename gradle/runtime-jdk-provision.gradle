import org.elasticsearch.gradle.OS
import org.elasticsearch.gradle.VersionProperties
import org.elasticsearch.gradle.info.BuildParams

apply plugin: 'elasticsearch.jdk-download'

jdks {
  provisioned_runtime {
    vendor = VersionProperties.bundledJdkVendor
    version = VersionProperties.getBundledJdk(OS.current().name().toLowerCase())
    platform = OS.current().name().toLowerCase()
  }
}

allprojects {
  if (project.path != ":build-tools") {
    //  Build tools doesn't support java 8 still used in CI
    project.tasks.withType(Test).configureEach { Test test ->
      if (BuildParams.getIsRuntimeJavaHomeSet()) {
        test.executable = "${BuildParams.runtimeJavaHome}/bin/java"
      } else {
        test.dependsOn(rootProject.jdks.provisioned_runtime)
        test.executable = rootProject.jdks.provisioned_runtime.getBinJavaPath()
      }
    }
  }
}

import org.elasticsearch.gradle.internal.info.BuildParams

/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */

apply plugin: 'elasticsearch.publish'

archivesBaseName = 'elasticsearch-core'

// we want to keep the JDKs in our IDEs set to JDK 8 until minimum JDK is bumped to 11 so we do not include this source set in our IDEs
if (!isEclipse) {
  sourceSets {
    java11 {
      java {
        srcDirs = ['src/main/java11']
      }
    }
  }

  configurations {
    java11Compile.extendsFrom(compile)
  }

  dependencies {
    java11Implementation sourceSets.main.output
  }

  tasks.named("compileJava11Java").configure {
    sourceCompatibility = 11
    targetCompatibility = 11
  }

  tasks.named("forbiddenApisJava11").configure {
    if (BuildParams.runtimeJavaVersion < JavaVersion.VERSION_11) {
      targetCompatibility = JavaVersion.VERSION_11.getMajorVersion()
    }
    replaceSignatureFiles 'jdk-signatures'
  }

  tasks.named("jar").configure {
    metaInf {
      into 'versions/11'
      from sourceSets.java11.output
    }
    manifest.attributes('Multi-Release': 'true')
  }
}

dependencies {
  // This dependency is used only by :libs:core for null-checking interop with other tools
  compileOnly "com.google.code.findbugs:jsr305:3.0.2"

  testImplementation "com.carrotsearch.randomizedtesting:randomizedtesting-runner:${versions.randomizedrunner}"
  testImplementation "junit:junit:${versions.junit}"
  testImplementation "org.hamcrest:hamcrest:${versions.hamcrest}"

  testImplementation(project(":test:framework")) {
    exclude group: 'org.elasticsearch', module: 'elasticsearch-core'
  }
}

tasks.named('forbiddenApisMain').configure {
  // :libs:elasticsearch-core does not depend on server
  // TODO: Need to decide how we want to handle for forbidden signatures with the changes to server
  replaceSignatureFiles 'jdk-signatures'
}

tasks.named("thirdPartyAudit").configure {
  ignoreMissingClasses(
          // from log4j
          'org/osgi/framework/AdaptPermission',
          'org/osgi/framework/AdminPermission',
          'org/osgi/framework/Bundle',
          'org/osgi/framework/BundleActivator',
          'org/osgi/framework/BundleContext',
          'org/osgi/framework/BundleEvent',
          'org/osgi/framework/SynchronousBundleListener',
          'org/osgi/framework/wiring/BundleWire',
          'org/osgi/framework/wiring/BundleWiring'
  )
}

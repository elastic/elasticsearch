/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the "Elastic License
 * 2.0", the "GNU Affero General Public License v3.0 only", and the "Server Side
 * Public License v 1"; you may not use this file except in compliance with, at
 * your election, the "Elastic License 2.0", the "GNU Affero General Public
 * License v3.0 only", or the "Server Side Public License, v 1".
 */

import org.elasticsearch.gradle.internal.precommit.CheckForbiddenApisTask

apply plugin: 'elasticsearch.build'
apply plugin: 'elasticsearch.publish'
apply plugin: 'elasticsearch.embedded-providers'
apply plugin: 'elasticsearch.mrjar'
apply plugin: 'elasticsearch.internal-test-artifact'

embeddedProviders {
  impl 'entitlement', project(':libs:entitlement:asm-provider')
}

dependencies {
  compileOnly project(':libs:core') // For @SuppressForbidden
  compileOnly project(':libs:logging')
  compileOnly project(":libs:x-content") // for parsing policy files
  compileOnly project(':libs:entitlement:bridge')
  testImplementation(project(":test:framework")) {
    exclude group: 'org.elasticsearch', module: 'entitlement'
  }

  testImplementation 'org.ow2.asm:asm:9.9'
  testImplementation 'org.ow2.asm:asm-util:9.9'
  testImplementation 'org.ow2.asm:asm-tree:9.9'
  testImplementation 'org.ow2.asm:asm-analysis:9.9'

  // guarding for intellij
  if (sourceSets.findByName("main23")) {
    main23CompileOnly project(path: ':libs:entitlement:bridge', configuration: 'java23')
  }
}

tasks.withType(CheckForbiddenApisTask).configureEach {
  replaceSignatureFiles 'jdk-signatures'
  exclude 'org/elasticsearch/entitlement/config/**.class'
}

tasks.named('javadoc') {
  // Our configuration references all kinds of nasty internal types
  exclude 'org/elasticsearch/entitlement/config/*'
  // Explcitly include the main entrypoint, as it's referenced in EntitlementBoostrap
  include 'org/elasticsearch/entitlement/config/MainInstrumentationProvider.java'
}

// We need to explicitly export these packages since we directly reference classes from them in our instrumentation config
def exportedPackages = [
  "jdk.jlink/jdk.tools.jlink.internal",
  "jdk.internal.vm.ci/jdk.vm.ci.services",
  "java.base/sun.net.www",
  "java.base/sun.net.www.protocol.ftp",
  "java.base/sun.net.www.protocol.file",
  "java.base/sun.net.www.protocol.jar",
  "java.base/sun.net.www.protocol.http",
  "java.base/sun.net.www.protocol.https",
  "java.base/sun.net.www.protocol.mailto",
  "java.base/sun.nio.ch",
  "java.base/jdk.internal.foreign",
  "java.base/jdk.internal.foreign.abi",
  "java.base/jdk.internal.foreign.layout",
  "java.net.http/jdk.internal.net.http",
  "jdk.jdi/com.sun.tools.jdi"
]

// invoke using ./gradlew :libs:entitlement:dumpInstrumentedMethods --args="<output file>"
tasks.register('dumpInstrumentedMethods', JavaExec) {
  classpath = sourceSets.test.runtimeClasspath
  mainClass = 'org.elasticsearch.entitlement.initialization.DynamicInstrumentationUtils'
  exportedPackages.each { jvmArgs("--add-opens", "${it}=ALL-UNNAMED")}
}

tasks.named('compileJava') {
  options.release.unset()
  exportedPackages.each { options.compilerArgs.addAll(["--add-exports", "${it}=org.elasticsearch.entitlement"])}
}

tasks.named('test') {
  exportedPackages.each { jvmArgs("--add-opens", "${it}=ALL-UNNAMED")}
}

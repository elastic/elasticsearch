/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the "Elastic License
 * 2.0", the "GNU Affero General Public License v3.0 only", and the "Server Side
 * Public License v 1"; you may not use this file except in compliance with, at
 * your election, the "Elastic License 2.0", the "GNU Affero General Public
 * License v3.0 only", or the "Server Side Public License, v 1".
 */

import org.elasticsearch.gradle.internal.info.BuildParams
import org.elasticsearch.gradle.internal.precommit.CheckForbiddenApisTask

apply plugin: 'elasticsearch.publish'
apply plugin: 'elasticsearch.build'
apply plugin: 'elasticsearch.mrjar'

dependencies {
  implementation project(':libs:elasticsearch-native')
  implementation project(':libs:elasticsearch-logging')
  implementation "org.apache.lucene:lucene-core:${versions.lucene}"

  testImplementation(project(":test:framework")) {
    exclude group: 'org.elasticsearch', module: 'elasticsearch-native'
  }
}

// compileMain21Java does not exist within idea (see MrJarPlugin) so we cannot reference directly by name
tasks.matching { it.name == "compileMain21Java" }.configureEach {
  options.compilerArgs << '--add-modules=jdk.incubator.vector'
  // we remove Werror, since incubating suppression (-Xlint:-incubating)
  // is only support since JDK 22
  options.compilerArgs -= '-Werror'
}

tasks.named('test').configure {
  if (BuildParams.getRuntimeJavaVersion().majorVersion.toInteger() >= 21) {
    jvmArgs '--add-modules=jdk.incubator.vector'
  }
}

tasks.withType(CheckForbiddenApisTask).configureEach {
  replaceSignatureFiles 'jdk-signatures'
}

// hack for now, fix the jarhell check for MRJar
tasks.named("jarHell").configure { enabled = false }

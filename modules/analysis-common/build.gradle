/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the "Elastic License
 * 2.0", the "GNU Affero General Public License v3.0 only", and the "Server Side
 * Public License v 1"; you may not use this file except in compliance with, at
 * your election, the "Elastic License 2.0", the "GNU Affero General Public
 * License v3.0 only", or the "Server Side Public License, v 1".
 */
import org.elasticsearch.gradle.internal.info.BuildParams

apply plugin: 'elasticsearch.internal-yaml-rest-test'
apply plugin: 'elasticsearch.yaml-rest-compat-test'
apply plugin: 'elasticsearch.internal-cluster-test'

esplugin {
  description = 'Adds "built in" analyzers to Elasticsearch.'
  classname = 'org.elasticsearch.analysis.common.CommonAnalysisPlugin'
  extendedPlugins = ['lang-painless']
}

restResources {
  restApi {
    include '_common', 'indices', 'index', 'cluster', 'search', 'nodes', 'bulk', 'termvectors', 'explain', 'count'
  }
}

dependencies {
  compileOnly project(':modules:lang-painless:spi')
  clusterModules project(':modules:reindex')
  clusterModules project(':modules:mapper-extras')
}

tasks.named("yamlRestTest").configure { task->
  if (buildParams.getRuntimeJavaVersion().get().majorVersion.toInteger() < 21) {
    // Requires at least Java 21
    systemProperty 'tests.rest.blacklist', [
      "analysis-common/50_char_filters/pattern_replace error handling (too complex pattern)"
    ].join(',')
  }
}

tasks.named("yamlRestTestV7CompatTransform").configure { task ->
  task.skipTest("indices.analyze/10_analyze/htmlStrip_deprecated", "Cleanup versioned deprecations in analysis #41560")
  task.skipTest("analysis-common/40_token_filters/delimited_payload_filter_error", "Remove preconfigured delimited_payload_filter #43686")
  task.skipTest("analysis-common/20_analyzers/standard_html_strip", "Cleanup versioned deprecations in analysis #41560")
  task.skipTest("search.query/50_queries_with_synonyms/Test common terms query with stacked tokens", "#42654 -  `common` query throws an exception")
}

artifacts {
  restTests(new File(projectDir, "src/yamlRestTest/resources/rest-api-spec/test"))
}

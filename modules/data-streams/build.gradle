import org.elasticsearch.gradle.testclusters.StandaloneRestIntegTestTask

apply plugin: 'elasticsearch.test-with-dependencies'
apply plugin: 'elasticsearch.internal-cluster-test'
apply plugin: 'elasticsearch.internal-yaml-rest-test'
apply plugin: 'elasticsearch.internal-java-rest-test'
apply plugin: 'elasticsearch.yaml-rest-compat-test'

esplugin {
  description 'Elasticsearch Expanded Pack Plugin - Data Streams'
  classname 'org.elasticsearch.datastreams.DataStreamsPlugin'
}

restResources {
  restApi {
    include 'bulk', 'count', 'search', '_common', 'indices', 'index', 'cluster', 'rank_eval', 'reindex', 'update_by_query', 'delete_by_query',
      'eql', 'data_stream', 'ingest', 'cat', 'capabilities'
  }
}

dependencies {
  testImplementation project(path: ':test:test-clusters')
  internalClusterTestImplementation project(":modules:mapper-extras")
}

tasks.withType(StandaloneRestIntegTestTask).configureEach {
  usesDefaultDistribution()
}

if (buildParams.inFipsJvm){
  // These fail in CI but only when run as part of checkPart2 and not individually.
  // Tracked in :
  tasks.named("javaRestTest").configure{enabled = false }
  tasks.named("yamlRestTest").configure{enabled = false }
}

if (buildParams.isSnapshotBuild() == false) {
  tasks.withType(Test).configureEach {
    systemProperty 'es.failure_store_feature_flag_enabled', 'true'
  }
}

tasks.named("yamlRestCompatTestTransform").configure({ task ->
  task.skipTest("data_stream/10_basic/Create hidden data stream", "warning does not exist for compatibility")
})

dependsOn(':test-testng')

apply plugin: 'java'
apply plugin: 'maven'

archivesBaseName = "$project.archivesBaseName"

processResources.doLast {
    Properties versionProps = new Properties();
    versionProps.setProperty("number", rootProject.version)
    versionProps.setProperty("date", buildTimeStr)

    File versionFile = new File(sourceSets.main.classesDir, "/org/elasticsearch/version.properties")
    versionFile.parentFile.mkdirs()
    versionFile.withOutputStream {
        versionProps.store(it, '')
    }
}


manifest.mainAttributes("Implementation-Title": "ElasticSearch", "Implementation-Version": rootProject.version, "Implementation-Date": buildTimeStr)

// no need to use the resource dir
sourceSets.main.resources.srcDirs 'src/main/java', rootProject.file('config')
sourceSets.test.resources.srcDir 'src/test/java'

// add the source files to the dist jar
jar {
    from sourceSets.main.allJava
}

dependencies {
    compile('jline:jline:0.9.94') { transitive = false }

    compile 'org.slf4j:slf4j-api:1.5.8'
    compile('org.slf4j:slf4j-log4j12:1.5.8') { transitive = false }
    compile('log4j:log4j:1.2.15') { transitive = false }

    compile 'joda-time:joda-time:1.6'
    compile 'com.google.collections:google-collections:1.0'

    compile 'org.codehaus.jackson:jackson-core-asl:1.5.0'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.5.0'

    compile 'aopalliance:aopalliance:1.0'
    compile 'com.google.inject:guice:2.0'
    compile 'com.google.inject.extensions:guice-assisted-inject:2.0'
    compile 'com.google.inject.extensions:guice-multibindings:2.0'

    compile 'org.apache.lucene:lucene-core:3.0.1'
    compile 'org.apache.lucene:lucene-analyzers:3.0.1'
    compile 'org.apache.lucene:lucene-queries:3.0.1'
    compile 'org.apache.lucene:lucene-fast-vector-highlighter:3.0.1'

    compile('jgroups:jgroups:2.9.0.GA') { transitive = false }
    compile('org.jboss.netty:netty:3.1.5.GA') { transitive = false }

    testCompile project(':test-testng')
    testCompile('org.testng:testng:5.10:jdk15') { transitive = false }
    testCompile 'org.hamcrest:hamcrest-all:1.1'
}

test {
    useTestNG()
    jmvArgs = ["-ea", "-Xmx1024m"]
    suiteName = project.name
    listeners = ["org.elasticsearch.util.testng.Listeners"]
    systemProperties["es.test.log.conf"] = System.getProperty("es.test.log.conf", "log4j-gradle.properties")
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: "file://localhost/" + rootProject.projectDir.absolutePath + "/build/maven/repository")
        snapshotRepository(url: "file://localhost/" + rootProject.projectDir.absolutePath + "/build/maven/snapshotRepository")

        pom.project {
            inceptionYear '2009'
            name 'elasticsearch'
            description 'Open Source, Distributed, RESTful Search Engine'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'git://github.com/elasticsearch/elasticsearch.git'
                developerConnection 'git@github.com:elasticsearch/elasticsearch.git'
                url 'http://github.com/elasticsearch/elasticsearch'
            }
        }

        pom.whenConfigured {pom ->
            pom.dependencies = pom.dependencies.findAll {dep -> dep.scope != 'test' } // removes the test scoped ones
            pom.dependencies = pom.dependencies.findAll {dep -> !dep.artifactId.contains('log4j') } // removes the test scoped ones
            pom.dependencies = pom.dependencies.findAll {dep -> !dep.artifactId.contains('jline') } // removes the test scoped ones
            pom.dependencies.findAll {dep -> dep.groupId == 'jgroups' }*.scope = 'runtime'
            pom.dependencies.findAll {dep -> dep.groupId == 'org.jboss.netty' }*.scope = 'runtime'
        }
    }
}
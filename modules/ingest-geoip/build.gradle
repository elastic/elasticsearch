/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os

esplugin {
  description 'Ingest processor that uses lookup geo data based on IP adresses using the MaxMind geo database'
  classname 'org.elasticsearch.ingest.geoip.IngestGeoIpPlugin'
}

dependencies {
  compile('com.maxmind.geoip2:geoip2:2.13.1')
  // geoip2 dependencies:
  compile("com.fasterxml.jackson.core:jackson-annotations:${versions.jackson}")
  compile("com.fasterxml.jackson.core:jackson-databind:${versions.jackson}")
  compile('com.maxmind.db:maxmind-db:1.3.1')

  testCompile 'org.elasticsearch:geolite2-databases:20191119'
}

restResources {
  restApi {
    includeCore '_common', 'indices', 'index', 'cluster', 'nodes', 'get', 'ingest'
  }
}

task copyDefaultGeoIp2DatabaseFiles(type: Copy) {
  from { zipTree(configurations.testCompile.files.find { it.name.contains('geolite2-databases') }) }
  into "${project.buildDir}/ingest-geoip"
  include "*.mmdb"
}

project.bundlePlugin.dependsOn(copyDefaultGeoIp2DatabaseFiles)

bundlePlugin {
  from("${project.buildDir}/ingest-geoip") {
    into '/'
  }
}

thirdPartyAudit.ignoreMissingClasses(
  // geoip WebServiceClient needs apache http client, but we're not using WebServiceClient:
  'org.apache.http.HttpEntity',
  'org.apache.http.HttpHost',
  'org.apache.http.HttpResponse',
  'org.apache.http.StatusLine',
  'org.apache.http.auth.UsernamePasswordCredentials',
  'org.apache.http.client.config.RequestConfig$Builder',
  'org.apache.http.client.config.RequestConfig',
  'org.apache.http.client.methods.CloseableHttpResponse',
  'org.apache.http.client.methods.HttpGet',
  'org.apache.http.client.utils.URIBuilder',
  'org.apache.http.impl.auth.BasicScheme',
  'org.apache.http.impl.client.CloseableHttpClient',
  'org.apache.http.impl.client.HttpClientBuilder',
  'org.apache.http.util.EntityUtils'
)

test {
  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    // Windows cannot cleanup database files properly unless it loads everything on heap.
    // See https://github.com/maxmind/MaxMind-DB-Reader-java#file-lock-on-windows for more information
    systemProperty 'es.geoip.load_db_on_heap', 'true'
  }
}

/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */

subprojects {
  apply plugin: 'elasticsearch.internal-testclusters'
}

configurations {
  allPlugins
}

// only configure immediate children of plugins dir
configure(subprojects.findAll { it.parent.path == project.path }) {
  group = 'org.elasticsearch.plugin'
  apply plugin: 'elasticsearch.internal-es-plugin'

  esplugin {
    // for local ES plugins, the name of the plugin is the same as the directory
    name project.name

    licenseFile rootProject.file('licenses/SSPL-1.0+ELASTIC-LICENSE-2.0.txt')
    noticeFile rootProject.file('NOTICE.txt')
  }


  parent.artifacts.add('allPlugins', tasks.named('bundlePlugin'))
}

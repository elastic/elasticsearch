dependsOn(':elasticsearch')

apply plugin: 'java'
apply plugin: 'maven'

archivesBaseName = "elasticsearch-cloud"

explodedDistDir = new File(distsDir, 'exploded')

manifest.mainAttributes("Implementation-Title": "ElasticSearch::Plugins::Cloud", "Implementation-Version": rootProject.version, "Implementation-Date": buildTimeStr)

configurations.compile.transitive = true
configurations.testCompile.transitive = true

// no need to use the resource dir
sourceSets.main.resources.srcDirs 'src/main/java'
sourceSets.test.resources.srcDirs 'src/test/java'

// add the source files to the dist jar
//jar {
//    from sourceSets.main.allJava
//}

configurations {
    dists
    distLib {
        visible = false
    }
}

repositories {
    mavenRepo urls: "http://jclouds.googlecode.com/svn/repo"
    mavenRepo urls: "http://jclouds.rimuhosting.com/maven2/snapshots"
    mavenRepo urls: "http://java-xmlbuilder.googlecode.com/svn/repo"
}

jcloudsVersion = "1.0-beta-5"

dependencies {
    compile project(':elasticsearch')
    compile("org.jclouds:jclouds-blobstore:$jcloudsVersion")
    compile("org.jclouds:jclouds-aws:$jcloudsVersion")
    compile("org.jclouds:jclouds-rackspace:$jcloudsVersion")
    compile("org.jclouds:jclouds-atmos:$jcloudsVersion")
    compile("org.jclouds:jclouds-azure:$jcloudsVersion")
    compile("org.jclouds:jclouds-gogrid:$jcloudsVersion")
    compile("org.jclouds:jclouds-vcloud:$jcloudsVersion")
    compile("org.jclouds:jclouds-rimuhosting:$jcloudsVersion")
    compile("org.jclouds:jclouds-terremark:$jcloudsVersion")
    compile("org.jclouds:jclouds-bluelock:$jcloudsVersion")
    compile("org.jclouds:jclouds-hostingdotcom:$jcloudsVersion")

    distLib("org.jclouds:jclouds-blobstore:$jcloudsVersion")
    distLib("org.jclouds:jclouds-aws:$jcloudsVersion")
    distLib("org.jclouds:jclouds-rackspace:$jcloudsVersion")
    distLib("org.jclouds:jclouds-atmos:$jcloudsVersion")
    distLib("org.jclouds:jclouds-azure:$jcloudsVersion")
    distLib("org.jclouds:jclouds-gogrid:$jcloudsVersion")
    distLib("org.jclouds:jclouds-vcloud:$jcloudsVersion")
    distLib("org.jclouds:jclouds-rimuhosting:$jcloudsVersion")
    distLib("org.jclouds:jclouds-terremark:$jcloudsVersion")
    distLib("org.jclouds:jclouds-bluelock:$jcloudsVersion")
    distLib("org.jclouds:jclouds-hostingdotcom:$jcloudsVersion")

    testCompile project(':test-testng')
    testCompile('org.testng:testng:5.10:jdk15') { transitive = false }
}

test {
    useTestNG()
    jmvArgs = ["-ea", "-Xmx1024m"]
    suiteName = project.name
    listeners = ["org.elasticsearch.util.testng.Listeners"]
    systemProperties["es.test.log.conf"] = System.getProperty("es.test.log.conf", "log4j-gradle.properties")
}

task explodedDist(dependsOn: [jar], description: 'Builds the plugin zip file') << {
    [explodedDistDir]*.mkdirs()

    copy {
        from configurations.distLib
        into explodedDistDir
    }

    // remove elasticsearch files (compile above adds the elasticsearch one)
    ant.delete { fileset(dir: explodedDistDir, includes: "elasticsearch-*.jar") }

    copy {
        from libsDir
        into explodedDistDir
    }

    ant.delete { fileset(dir: explodedDistDir, includes: "elasticsearch-*-javadoc.jar") }
    ant.delete { fileset(dir: explodedDistDir, includes: "elasticsearch-*-sources.jar") }
}

task zip(type: Zip, dependsOn: ['explodedDist']) {
    from(explodedDistDir) {
    }
}

task release(dependsOn: [zip]) << {
    ant.delete(dir: explodedDistDir)
    copy {
        from distsDir
        into(new File(rootProject.distsDir, "plugins"))
    }
}

configurations {
    deployerJars
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-http:1.0-beta-2"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: rootProject.mavenRepoUrl) {
            authentication(userName: rootProject.mavenRepoUser, password: rootProject.mavenRepoPass)
        }
        snapshotRepository(url: rootProject.mavenSnapshotRepoUrl) {
            authentication(userName: rootProject.mavenRepoUser, password: rootProject.mavenRepoPass)
        }

        pom.project {
            inceptionYear '2009'
            name 'elasticsearch-plugins-cloud'
            description 'Clouud Plugin for ElasticSearch'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'git://github.com/elasticsearch/elasticsearch.git'
                developerConnection 'git@github.com:elasticsearch/elasticsearch.git'
                url 'http://github.com/elasticsearch/elasticsearch'
            }
        }

        pom.whenConfigured {pom ->
            pom.dependencies = pom.dependencies.findAll {dep -> dep.scope != 'test' } // removes the test scoped ones
        }
    }
}

import org.elasticsearch.gradle.LoggedExec
import org.elasticsearch.gradle.info.BuildParams

/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

esplugin {
  description 'The Azure Classic Discovery plugin allows to use Azure Classic API for the unicast discovery mechanism'
  classname 'org.elasticsearch.plugin.discovery.azure.classic.AzureDiscoveryPlugin'
}

versions << [
  'azure' : '0.9.3',
  'jersey': '1.13'
]

dependencies {
  compile "com.microsoft.azure:azure-svc-mgmt-compute:${versions.azure}"
  compile "com.microsoft.azure:azure-core:${versions.azure}"
  compile "org.apache.httpcomponents:httpclient:${versions.httpclient}"
  compile "org.apache.httpcomponents:httpcore:${versions.httpcore}"
  compile "commons-logging:commons-logging:${versions.commonslogging}"
  compile "org.apache.logging.log4j:log4j-1.2-api:${versions.log4j}"
  compile "commons-codec:commons-codec:${versions.commonscodec}"
  compile "commons-lang:commons-lang:2.6"
  compile "commons-io:commons-io:2.4"
  compile 'javax.mail:mail:1.4.5'
  compile 'javax.inject:javax.inject:1'
  compile "com.sun.jersey:jersey-client:${versions.jersey}"
  compile "com.sun.jersey:jersey-core:${versions.jersey}"
  compile "com.sun.jersey:jersey-json:${versions.jersey}"
  compile 'org.codehaus.jettison:jettison:1.1'
  compile 'com.sun.xml.bind:jaxb-impl:2.2.3-1'
  compile 'org.codehaus.jackson:jackson-core-asl:1.9.2'
  compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.2'
  compile 'org.codehaus.jackson:jackson-jaxrs:1.9.2'
  compile 'org.codehaus.jackson:jackson-xc:1.9.2'

  // HACK: javax.xml.bind was removed from default modules in java 9, so we pull the api in here,
  // and whitelist this hack in JarHell
  compile 'javax.xml.bind:jaxb-api:2.2.2'
}

restResources {
  restApi {
    includeCore '_common', 'cluster', 'nodes'
  }
}
// needed to be consistent with ssl host checking
String host = InetAddress.getLoopbackAddress().getHostAddress()

// location of keystore and files to generate it
File keystore = new File(project.buildDir, 'keystore/test-node.jks')

// generate the keystore
task createKey(type: LoggedExec) {
  doFirst {
    project.delete(keystore.parentFile)
    keystore.parentFile.mkdirs()
  }
  outputs.file(keystore).withPropertyName('keystoreFile')
  executable = "${BuildParams.runtimeJavaHome}/bin/keytool"
  standardInput = new ByteArrayInputStream('FirstName LastName\nUnit\nOrganization\nCity\nState\nNL\nyes\n\n'.getBytes('UTF-8'))
  args '-genkey',
    '-alias', 'test-node',
    '-keystore', keystore,
    '-keyalg', 'RSA',
    '-keysize', '2048',
    '-validity', '712',
    '-dname', 'CN=' + host,
    '-keypass', 'keypass',
    '-storepass', 'keypass'
}

// add keystore to test classpath: it expects it there
processTestResources {
  from createKey
}

normalization {
  runtimeClasspath {
    ignore 'test-node.jks'
  }
}

dependencyLicenses {
  mapping from: /azure-.*/, to: 'azure'
  mapping from: /jackson-.*/, to: 'jackson'
  mapping from: /jersey-.*/, to: 'jersey'
  mapping from: /jaxb-.*/, to: 'jaxb'
}

thirdPartyAudit.ignoreMissingClasses(
  'javax.activation.ActivationDataFlavor',
  'javax.activation.DataContentHandler',
  'javax.activation.DataHandler',
  'javax.activation.DataSource',
  'javax.activation.FileDataSource',
  'javax.activation.FileTypeMap',
  'javax.activation.MimeType',
  'javax.activation.MimeTypeParseException',
  'javax.servlet.ServletContextEvent',
  'javax.servlet.ServletContextListener',
  'org.apache.avalon.framework.logger.Logger',
  'org.apache.log.Hierarchy',
  'org.apache.log.Logger',
  'org.eclipse.persistence.descriptors.ClassDescriptor',
  'org.eclipse.persistence.internal.oxm.MappingNodeValue',
  'org.eclipse.persistence.internal.oxm.TreeObjectBuilder',
  'org.eclipse.persistence.internal.oxm.XPathFragment',
  'org.eclipse.persistence.internal.oxm.XPathNode',
  'org.eclipse.persistence.internal.queries.ContainerPolicy',
  'org.eclipse.persistence.jaxb.JAXBContext',
  'org.eclipse.persistence.jaxb.JAXBHelper',
  'org.eclipse.persistence.mappings.DatabaseMapping',
  'org.eclipse.persistence.mappings.converters.TypeConversionConverter',
  'org.eclipse.persistence.mappings.foundation.AbstractCompositeDirectCollectionMapping',
  'org.eclipse.persistence.oxm.XMLContext',
  'org.eclipse.persistence.oxm.XMLDescriptor',
  'org.eclipse.persistence.oxm.XMLField',
  'org.eclipse.persistence.oxm.mappings.XMLCompositeCollectionMapping',
  'org.eclipse.persistence.sessions.DatabaseSession',
  'org.jvnet.fastinfoset.VocabularyApplicationData',
  'org.jvnet.staxex.Base64Data',
  'org.jvnet.staxex.XMLStreamReaderEx',
  'org.jvnet.staxex.XMLStreamWriterEx',
  'org.osgi.framework.Bundle',
  'org.osgi.framework.BundleActivator',
  'org.osgi.framework.BundleContext',
  'org.osgi.framework.BundleEvent',
  'org.osgi.framework.SynchronousBundleListener',
  'com.sun.xml.fastinfoset.stax.StAXDocumentParser',
  'com.sun.xml.fastinfoset.stax.StAXDocumentSerializer'
)

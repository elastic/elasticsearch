apply plugin: 'elasticsearch.internal-yaml-rest-test'
apply plugin: 'elasticsearch.internal-cluster-test'

esplugin {
  description 'The Google Compute Engine (GCE) Discovery plugin allows to use GCE API for the unicast discovery mechanism.'
  classname 'org.elasticsearch.plugin.discovery.gce.GceDiscoveryPlugin'
}

versions << [
  'google': '1.23.0'
]

dependencies {
  api "com.google.apis:google-api-services-compute:v1-rev160-${versions.google}"
  api "com.google.api-client:google-api-client:${versions.google}"
  api "com.google.oauth-client:google-oauth-client:${versions.google}"
  api "com.google.http-client:google-http-client:${versions.google}"
  api "com.google.http-client:google-http-client-jackson2:${versions.google}"
  api "com.fasterxml.jackson.core:jackson-core:${versions.jackson}"
  api 'com.google.code.findbugs:jsr305:1.3.9'
  api "commons-logging:commons-logging:${versions.commonslogging}"
  api "org.apache.logging.log4j:log4j-1.2-api:${versions.log4j}"
  api "commons-codec:commons-codec:${versions.commonscodec}"
}

restResources {
  restApi {
    include '_common', 'cluster', 'nodes'
  }
}

tasks.named("dependencyLicenses").configure {
  mapping from: /google-.*/, to: 'google'
  mapping from: /jackson-.*/, to: 'jackson'
}

tasks.named("check").configure {
  // also execute the QA tests when testing the plugin
  dependsOn 'qa:gce:check'
}

tasks.named("test").configure {
  // this is needed for insecure plugins, remove if possible!
  systemProperty 'tests.artifact', project.name
}

tasks.named("thirdPartyAudit").configure {
  ignoreMissingClasses(
          // classes are missing
          'com.google.common.base.Splitter',
          'com.google.common.collect.Lists',
          'javax.servlet.ServletContextEvent',
          'javax.servlet.ServletContextListener',
          'org.apache.avalon.framework.logger.Logger',
          'org.apache.log.Hierarchy',
          'org.apache.log.Logger',
          'org.apache.avalon.framework.logger.Logger',
          'javax.jms.Message',
          'org.apache.http.ConnectionReuseStrategy',
          'org.apache.http.Header',
          'org.apache.http.HttpEntity',
          'org.apache.http.HttpEntityEnclosingRequest',
          'org.apache.http.HttpHost',
          'org.apache.http.HttpRequest',
          'org.apache.http.HttpResponse',
          'org.apache.http.HttpVersion',
          'org.apache.http.RequestLine',
          'org.apache.http.StatusLine',
          'org.apache.http.client.AuthenticationHandler',
          'org.apache.http.client.HttpClient',
          'org.apache.http.client.HttpRequestRetryHandler',
          'org.apache.http.client.RedirectHandler',
          'org.apache.http.client.RequestDirector',
          'org.apache.http.client.UserTokenHandler',
          'org.apache.http.client.methods.HttpEntityEnclosingRequestBase',
          'org.apache.http.client.methods.HttpRequestBase',
          'org.apache.http.conn.ClientConnectionManager',
          'org.apache.http.conn.ConnectionKeepAliveStrategy',
          'org.apache.http.conn.params.ConnManagerParams',
          'org.apache.http.conn.params.ConnRouteParams',
          'org.apache.http.conn.routing.HttpRoutePlanner',
          'org.apache.http.conn.scheme.PlainSocketFactory',
          'org.apache.http.conn.scheme.SchemeRegistry',
          'org.apache.http.conn.ssl.SSLSocketFactory',
          'org.apache.http.conn.ssl.X509HostnameVerifier',
          'org.apache.http.entity.AbstractHttpEntity',
          'org.apache.http.impl.client.DefaultHttpClient',
          'org.apache.http.params.HttpConnectionParams',
          'org.apache.http.params.HttpParams',
          'org.apache.http.params.HttpProtocolParams',
          'org.apache.http.protocol.HttpContext',
          'org.apache.http.protocol.HttpProcessor',
          'org.apache.http.protocol.HttpRequestExecutor'
  )
}

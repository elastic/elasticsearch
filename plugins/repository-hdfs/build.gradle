/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */

import org.apache.tools.ant.taskdefs.condition.Os
import org.elasticsearch.gradle.internal.info.BuildParams
import org.elasticsearch.gradle.internal.test.RestIntegTestTask

import java.nio.file.Files
import java.nio.file.Path
import java.nio.file.Paths

import static org.elasticsearch.gradle.PropertyNormalization.IGNORE_VALUE

apply plugin: 'elasticsearch.test.fixtures'
apply plugin: 'elasticsearch.rest-resources'
apply plugin: 'elasticsearch.rest-test'

esplugin {
  description 'The HDFS repository plugin adds support for Hadoop Distributed File-System (HDFS) repositories.'
  classname 'org.elasticsearch.repositories.hdfs.HdfsPlugin'
}

versions << [
  'hadoop2': '2.8.5'
]

testFixtures.useFixture ":test:fixtures:krb5kdc-fixture", "hdfs"

configurations {
  hdfsFixture
}

dependencies {
  api "org.apache.hadoop:hadoop-client:${versions.hadoop2}"
  api project(path: 'hadoop-common', configuration: 'shadow')
  if (isEclipse) {
    /*
     * Eclipse can't pick up the shadow dependency so we point it at *something*
     * so it can compile things.
     */
    api project(path: 'hadoop-common')
  }
  api "org.apache.hadoop:hadoop-annotations:${versions.hadoop2}"
  api "org.apache.hadoop:hadoop-auth:${versions.hadoop2}"
  api "org.apache.hadoop:hadoop-hdfs:${versions.hadoop2}"
  api "org.apache.hadoop:hadoop-hdfs-client:${versions.hadoop2}"
  api 'org.apache.htrace:htrace-core4:4.0.1-incubating'
  runtimeOnly 'com.google.guava:guava:11.0.2'
  api 'com.google.protobuf:protobuf-java:2.5.0'
  api 'commons-logging:commons-logging:1.1.3'
  api "org.apache.logging.log4j:log4j-1.2-api:${versions.log4j}"
  api 'commons-cli:commons-cli:1.2'
  api "commons-codec:commons-codec:${versions.commonscodec}"
  api 'commons-collections:commons-collections:3.2.2'
  api 'commons-configuration:commons-configuration:1.6'
  api 'commons-io:commons-io:2.4'
  api 'commons-lang:commons-lang:2.6'
  api 'javax.servlet:servlet-api:2.5'
  api "org.slf4j:slf4j-api:${versions.slf4j}"
  api "org.apache.logging.log4j:log4j-slf4j-impl:${versions.log4j}"

  hdfsFixture project(':test:fixtures:hdfs-fixture')
  // Set the keytab files in the classpath so that we can access them from test code without the security manager
  // freaking out.
  if (isEclipse == false) {
    testRuntimeOnly files(project(':test:fixtures:krb5kdc-fixture').ext.krb5Keytabs("hdfs", "hdfs_hdfs.build.elastic.co.keytab").parent) {
      builtBy ":test:fixtures:krb5kdc-fixture:preProcessFixture"
    }
  }
}

restResources {
  restApi {
    include '_common', 'cluster', 'nodes', 'indices', 'index', 'snapshot'
  }
}

normalization {
  runtimeClasspath {
    // ignore generated keytab files for the purposes of build avoidance
    ignore '*.keytab'
    // ignore fixture ports file which is on the classpath primarily to pacify the security manager
    ignore 'ports'
  }
}

tasks.named("dependencyLicenses").configure {
  mapping from: /hadoop-.*/, to: 'hadoop'
}

tasks.named("integTest").configure {
  dependsOn(project.tasks.named("bundlePlugin"))
}

testClusters.matching { it.name == "integTest" }.configureEach {
  plugin(project.tasks.bundlePlugin.archiveFile)
}

String realm = "BUILD.ELASTIC.CO"
String krb5conf = project(':test:fixtures:krb5kdc-fixture').ext.krb5Conf("hdfs")

// Create HDFS File System Testing Fixtures for HA/Secure combinations
for (String fixtureName : ['hdfsFixture', 'haHdfsFixture', 'secureHdfsFixture', 'secureHaHdfsFixture']) {
  project.tasks.register(fixtureName, org.elasticsearch.gradle.internal.test.AntFixture) {
    dependsOn project.configurations.hdfsFixture, project(':test:fixtures:krb5kdc-fixture').tasks.postProcessFixture
    executable = "${BuildParams.runtimeJavaHome}/bin/java"
    env 'CLASSPATH', "${-> project.configurations.hdfsFixture.asPath}"
    maxWaitInSeconds 60
    onlyIf { BuildParams.inFipsJvm == false }
    waitCondition = { fixture, ant ->
      // the hdfs.MiniHDFS fixture writes the ports file when
      // it's ready, so we can just wait for the file to exist
      return fixture.portsFile.exists()
    }
    final List<String> miniHDFSArgs = []

    // If it's a secure fixture, then depend on Kerberos Fixture and principals + add the krb5conf to the JVM options
    if (name.equals('secureHdfsFixture') || name.equals('secureHaHdfsFixture')) {
      miniHDFSArgs.add("-Djava.security.krb5.conf=${krb5conf}")
      onlyIf { BuildParams.runtimeJavaVersion < JavaVersion.VERSION_16 }
    }
    // If it's an HA fixture, set a nameservice to use in the JVM options
    if (name.equals('haHdfsFixture') || name.equals('secureHaHdfsFixture')) {
      miniHDFSArgs.add("-Dha-nameservice=ha-hdfs")
    }

    // Common options
    miniHDFSArgs.add('hdfs.MiniHDFS')
    miniHDFSArgs.add(baseDir)

    // If it's a secure fixture, then set the principal name and keytab locations to use for auth.
    if (name.equals('secureHdfsFixture') || name.equals('secureHaHdfsFixture')) {
      miniHDFSArgs.add("hdfs/hdfs.build.elastic.co@${realm}")
      miniHDFSArgs.add(
        project(':test:fixtures:krb5kdc-fixture').ext.krb5Keytabs("hdfs", "hdfs_hdfs.build.elastic.co.keytab")
      )
    }

    args miniHDFSArgs.toArray()
  }
}

Set disabledIntegTestTaskNames = []

for (String integTestTaskName : ['integTestHa', 'integTestSecure', 'integTestSecureHa']) {
  def testTask = tasks.register(integTestTaskName, RestIntegTestTask) {
    description = "Runs rest tests against an elasticsearch cluster with HDFS."
    dependsOn("bundlePlugin")

    if (disabledIntegTestTaskNames.contains(name)) {
      enabled = false;
    }

    if (name.contains("Secure")) {
      onlyIf { BuildParams.runtimeJavaVersion < JavaVersion.VERSION_16 }
      if (name.contains("Ha")) {
        dependsOn "secureHaHdfsFixture"
      } else {
        dependsOn "secureHdfsFixture"
      }
    }

      onlyIf { BuildParams.inFipsJvm == false }
      if (name.contains("Ha")) {
        Path portsFile
        File portsFileDir = file("${workingDir}/hdfsFixture")
        if (name.contains("Secure")) {
          portsFile = buildDir.toPath()
            .resolve("fixtures")
            .resolve("secureHaHdfsFixture")
            .resolve("ports")
        } else {
          portsFile = buildDir.toPath()
            .resolve("fixtures")
            .resolve("haHdfsFixture")
            .resolve("ports")
        }
        nonInputProperties.systemProperty "test.hdfs-fixture.ports", file("$portsFileDir/ports")
        classpath += files(portsFileDir)
        // Copy ports file to separate location which is placed on the test classpath
        doFirst {
          mkdir(portsFileDir)
          copy {
            from portsFile
            into portsFileDir
          }
        }
      }

      if (name.contains("Secure")) {
        if (disabledIntegTestTaskNames.contains(name) == false) {
          nonInputProperties.systemProperty "test.krb5.principal.es", "elasticsearch@${realm}"
          nonInputProperties.systemProperty "test.krb5.principal.hdfs", "hdfs/hdfs.build.elastic.co@${realm}"
          jvmArgs "-Djava.security.krb5.conf=${krb5conf}",
                  "--add-exports=java.security.jgss/sun.security.krb5=ALL-UNNAMED"
          nonInputProperties.systemProperty(
            "test.krb5.keytab.hdfs",
            project(':test:fixtures:krb5kdc-fixture').ext.krb5Keytabs("hdfs", "hdfs_hdfs.build.elastic.co.keytab")
          )
        }
      }
  }

  testClusters.matching { it.name == testTask.name}.configureEach {
    plugin(bundlePlugin.archiveFile)
    if (integTestTaskName.contains("Secure")) {
      systemProperty "java.security.krb5.conf", krb5conf
      extraConfigFile(
        "repository-hdfs/krb5.keytab",
        file("${project(':test:fixtures:krb5kdc-fixture').ext.krb5Keytabs("hdfs", "elasticsearch.keytab")}"), IGNORE_VALUE
      )
    }
  }
}

// Determine HDFS Fixture compatibility for the current build environment.
boolean fixtureSupported = false
if (Os.isFamily(Os.FAMILY_WINDOWS)) {
  // hdfs fixture will not start without hadoop native libraries on windows
  String nativePath = System.getenv("HADOOP_HOME")
  if (nativePath != null) {
    Path path = Paths.get(nativePath);
    if (Files.isDirectory(path) &&
      Files.exists(path.resolve("bin").resolve("winutils.exe")) &&
      Files.exists(path.resolve("bin").resolve("hadoop.dll")) &&
      Files.exists(path.resolve("bin").resolve("hdfs.dll"))) {
      fixtureSupported = true
    } else {
      throw new IllegalStateException("HADOOP_HOME: ${path} is invalid, does not contain hadoop native libraries in \$HADOOP_HOME/bin");
    }
  }
} else {
  fixtureSupported = true
}

boolean legalPath = rootProject.rootDir.toString().contains(" ") == false
if (legalPath == false) {
  fixtureSupported = false
}

// Always ignore HA integration tests in the normal integration test runner, they are included below as
// part of their own HA-specific integration test tasks.
tasks.named("integTest").configure {
  onlyIf { BuildParams.inFipsJvm == false }
  exclude('**/Ha*TestSuiteIT.class')
}

if (fixtureSupported) {
  // Check depends on the HA test. Already depends on the standard test.
  tasks.named("check").configure {
    dependsOn("integTestHa")
  }

  // Both standard and HA tests depend on their respective HDFS fixtures
  tasks.named("integTest").configure {
    dependsOn "hdfsFixture"

    // The normal test runner only runs the standard hdfs rest tests
    systemProperty 'tests.rest.suite', 'hdfs_repository'
  }
  tasks.named("integTestHa").configure {
    dependsOn "haHdfsFixture"
    // Only include the HA integration tests for the HA test task
    setIncludes(['**/Ha*TestSuiteIT.class'])

  }

} else {
  if (legalPath) {
    logger.warn("hdfsFixture unsupported, please set HADOOP_HOME and put HADOOP_HOME\\bin in PATH")
  } else {
    logger.warn("hdfsFixture unsupported since there are spaces in the path: '" + rootProject.rootDir.toString() + "'")
  }

  // The normal integration test runner will just test that the plugin loads
  tasks.named("integTest").configure {
    systemProperty 'tests.rest.suite', 'hdfs_repository/10_basic'
  }
  // HA fixture is unsupported. Don't run them.
  tasks.named("integTestHa").configure {
    setEnabled(false)
  }
}

tasks.named("check").configure {
  dependsOn("integTestSecure", "integTestSecureHa")
}

// Run just the secure hdfs rest test suite.
tasks.named("integTestSecure").configure {
  systemProperty 'tests.rest.suite', 'secure_hdfs_repository'
  // Ignore HA integration Tests. They are included below as part of integTestSecureHa test runner.
  exclude('**/Ha*TestSuiteIT.class')
}
// Only include the HA integration tests for the HA test task
tasks.named("integTestSecureHa").configure {
  setIncludes(['**/Ha*TestSuiteIT.class'])
}

tasks.named("thirdPartyAudit").configure {
  ignoreMissingClasses()
  ignoreViolations(
    // internal java api: sun.misc.Unsafe
    'com.google.common.primitives.UnsignedBytes$LexicographicalComparatorHolder$UnsafeComparator',
    'com.google.common.primitives.UnsignedBytes$LexicographicalComparatorHolder$UnsafeComparator$1',
    'org.apache.hadoop.hdfs.shortcircuit.ShortCircuitShm',
    'org.apache.hadoop.hdfs.shortcircuit.ShortCircuitShm$Slot',
  )
}

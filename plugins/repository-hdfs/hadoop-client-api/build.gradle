import org.gradle.api.file.ArchiveOperations

apply plugin: 'elasticsearch.java'

configurations {
  thejar {
    canBeResolved = true
  }
  patcher
}

dependencies {
  thejar("org.apache.hadoop:hadoop-client-api:${project.parent.versions.hadoop}") {
    transitive = false
  }
  patcher(project(':plugins:repository-hdfs:hadoop-common-patcher'))
}

def outputDir = layout.buildDirectory.dir("patched-classes")

def patchTask = tasks.register("patchClasses", JavaExec) {
  inputs.files(configurations.thejar).withPathSensitivity(PathSensitivity.RELATIVE)
  outputs.dir(outputDir)
  classpath = configurations.patcher
  mainClass = 'org.elasticsearch.hdfs.patch.HdfsClassPatcher'
  def thejar = configurations.thejar
  doFirst {
    args(thejar.singleFile, outputDir.get().asFile)
  }
}

interface InjectedArchiveOps {
  @Inject ArchiveOperations getArchiveOperations()
}

tasks.named('jar').configure {
  dependsOn(configurations.thejar)
  def injected = project.objects.newInstance(InjectedArchiveOps)
  def thejar = configurations.thejar
  from(patchTask)
  from({ injected.getArchiveOperations().zipTree(thejar.singleFile) }) {
    eachFile {
      if (outputDir.get().file(it.relativePath.pathString).asFile.exists()) {
        it.exclude()
      }
    }
  }
}

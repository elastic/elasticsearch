apply plugin: 'elasticsearch.rest-test'

dependencies {
  testCompile project(path: ':x-plugins:elasticsearch', configuration: 'runtime')
}

String outputDir = "generated-resources/${project.name}"
task copyXPackPluginProps(type: Copy) {
  from project(':x-plugins:elasticsearch').file('src/main/plugin-metadata')
  from project(':x-plugins:elasticsearch').tasks.pluginProperties
  into outputDir
}
project.sourceSets.test.output.dir(outputDir, builtBy: copyXPackPluginProps)

integTest {
  cluster {
    plugin ':x-plugins:elasticsearch'
    setting 'xpack.security.audit.enabled', 'true'
    setting 'xpack.security.audit.outputs', 'index'
    setting 'logger.level', 'DEBUG'
    setupCommand 'setupDummyUser',
                 'bin/x-pack/users', 'useradd', 'test_user', '-p', 'changeme', '-r', 'superuser'
    waitCondition = { node, ant ->
      File tmpFile = new File(node.cwd, 'wait.success')
      ant.get(src: "http://${node.httpUri()}",
              dest: tmpFile.toString(),
              username: 'test_user',
              password: 'changeme',
              ignoreerrors: true,
              retries: 10)
      return tmpFile.exists()
    }
  }
}


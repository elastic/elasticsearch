apply plugin: 'elasticsearch.internal-yaml-rest-test'

// This subproject copies a subset of the rest-api-spec rest tests and runs them in a slightly
// modified way on two clusters connected via CCS. All operations except searches and a few other
// APIs that support CCS are run against the remote "write" cluster where all indexed data will live.
// All search requests however are run against the local cluster connected via CCS to the remote
// cluster. The test runner modifies index names on these API calls to route to the remote cluster
// and also modifies certain "match" sections to expect index names with the remote cluster prefix
// on the fly while running these tests.

restResources {
  restApi {
    include '_common', 'bulk', 'count', 'cluster', 'field_caps', 'knn_search', 'index', 'indices', 'msearch',
      'search', 'async_search', 'graph', '*_point_in_time', 'info', 'scroll', 'clear_scroll'
  }
  restTests {
    includeCore 'field_caps', 'msearch', 'search', 'suggest', 'scroll', "indices.resolve_index"
    includeXpack 'async_search'
  }
}

dependencies {
  clusterModules project(':x-pack:plugin:async-search')
  clusterModules project(':modules:mapper-extras')
  clusterModules project(':modules:aggregations')
}

tasks.named("yamlRestTest") {
  systemProperty 'tests.rest.blacklist',
    [
      'search/150_rewrite_on_coordinator/Ensure that we fetch the document only once', // terms lookup query with index
      'search/170_terms_query/Terms Query with No.of terms exceeding index.max_terms_count should FAIL', // terms lookup query with index
      'search/350_point_in_time/basic', // [indices] cannot be used with point in time
      'search/350_point_in_time/point-in-time with slicing', // [indices] cannot be used with point in time
      'search/350_point_in_time/msearch', // [indices] cannot be used with point in time
      'search/350_point_in_time/wildcard', // [indices] cannot be used with point in time
      'search.aggregation/220_filters_bucket/cache busting', // node_selector?
      'search.aggregation/220_filters_bucket/cache hits', // node_selector?
      'search.aggregation/50_filter/Standard queries get cached',
      'search.aggregation/50_filter/Terms lookup gets cached', // terms lookup by "index" doesn't seem to work correctly
      'search.aggregation/70_adjacency_matrix/Terms lookup', // terms lookup by "index" doesn't seem to work correctly
      'async_search/20-with-poin-in-time/Async search with point in time' // [indices] cannot be used with point in time
    ].join(',')
}


import org.elasticsearch.gradle.internal.info.BuildParams
import org.elasticsearch.gradle.util.GradleUtils

apply plugin: 'elasticsearch.java-rest-test'
apply plugin: 'elasticsearch.internal-es-plugin'

esplugin {
  description 'Die with dignity plugin'
  classname 'org.elasticsearch.DieWithDignityPlugin'
}

// let the javaRestTest see the classpath of main
GradleUtils.extendSourceSet(project, "main", "javaRestTest", tasks.named("javaRestTest"))

tasks.named("javaRestTest").configure {
  systemProperty 'tests.security.manager', 'false'
  systemProperty 'tests.system_call_filter', 'false'
  nonInputProperties.systemProperty 'log', "${-> testClusters.javaRestTest.singleNode().getServerLog()}"
  systemProperty 'runtime.java.home', BuildParams.runtimeJavaHome
}

testClusters.matching { it.name == "javaRestTest" }.configureEach {
  systemProperty "die.with.dignity.test", "whatever"
  setting 'xpack.security.enabled', 'true'
  user username: 'admin', password: 'admin-password', role: 'superuser'
}

tasks.named("test").configure {
  enabled = false
}

tasks.named('splitPackagesAudit').configure {
  // these should be moved to an actual package, not the root package
  ignoreClasses 'org.elasticsearch.DieWithDignityPlugin',
    'org.elasticsearch.RestDieWithDignityAction'
}

/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0 and the Server Side Public License, v 1; you may not use this file except
 * in compliance with, at your election, the Elastic License 2.0 or the Server
 * Side Public License, v 1.
 */


import org.elasticsearch.gradle.Version
import org.elasticsearch.gradle.VersionProperties
import org.elasticsearch.gradle.internal.info.BuildParams
import org.elasticsearch.gradle.testclusters.StandaloneRestIntegTestTask

apply plugin: 'elasticsearch.internal-testclusters'
apply plugin: 'elasticsearch.standalone-rest-test'
apply plugin: 'elasticsearch.bwc-test'
apply plugin: 'elasticsearch.rest-resources'

dependencies {
  restTestConfig project(path: ':modules:aggregations', configuration: 'restTests')
}

restResources {
  restTests {
    includeCore '*'
  }
}

BuildParams.bwcVersions.withWireCompatible { bwcVersion, baseName ->

  if (bwcVersion != VersionProperties.getElasticsearchVersion()) {
    /* This project runs the core REST tests against a 4 node cluster where two of
     the nodes has a different minor.  */
    def baseCluster = testClusters.register(baseName) {
      versions = [bwcVersion.toString(), project.version]
      numberOfNodes = 4
      setting 'path.repo', "${buildDir}/cluster/shared/repo/${baseName}"
      setting 'xpack.security.enabled', 'false'
      requiresFeature 'es.index_mode_feature_flag_registered', Version.fromString("8.0.0")
    }

    tasks.register("${baseName}#mixedClusterTest", StandaloneRestIntegTestTask) {
      useCluster baseCluster
      mustRunAfter("precommit")
      doFirst {
        delete("${buildDir}/cluster/shared/repo/${baseName}")
        // Getting the endpoints causes a wait for the cluster
        println "Test cluster endpoints are: ${-> baseCluster.get().allHttpSocketURI.join(",")}"
        println "Upgrading one node to create a mixed cluster"
        baseCluster.get().nextNodeToNextVersion()
        // Getting the endpoints causes a wait for the cluster
        println "Upgrade complete, endpoints are: ${-> baseCluster.get().allHttpSocketURI.join(",")}"
        println "Upgrading another node to create a mixed cluster"
        baseCluster.get().nextNodeToNextVersion()
        nonInputProperties.systemProperty('tests.rest.cluster', baseCluster.map(c -> c.allHttpSocketURI.join(",")))
        nonInputProperties.systemProperty('tests.clustername', baseName)
      }
      systemProperty 'tests.path.repo', "${buildDir}/cluster/shared/repo/${baseName}"
      onlyIf("BWC tests disabled") { project.bwc_tests_enabled }
    }

    tasks.register(bwcTaskName(bwcVersion)) {
      dependsOn "${baseName}#mixedClusterTest"
    }

    tasks.named("${baseName}#mixedClusterTest").configure {
      systemProperty 'tests.rest.blacklist', [
        'aggregations/percentile_ranks_tdigest_metric/basic',
        'aggregations/percentile_ranks_tdigest_metric/filtered',
        'aggregations/percentile_ranks_tdigest_metric/non-keyed test',
        'aggregations/percentiles_tdigest_metric/Basic test',
        'aggregations/percentiles_tdigest_metric/Only aggs test',
        'aggregations/percentiles_tdigest_metric/Filtered test',
        'aggregations/percentiles_tdigest_metric/Metadata test',
        'aggregations/percentiles_tdigest_metric/Explicit Percents test',
        'aggregations/percentiles_tdigest_metric/Non-keyed test',
      ].join(',')
    }
  }
}

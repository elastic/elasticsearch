/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the "Elastic License
 * 2.0", the "GNU Affero General Public License v3.0 only", and the "Server Side
 * Public License v 1"; you may not use this file except in compliance with, at
 * your election, the "Elastic License 2.0", the "GNU Affero General Public
 * License v3.0 only", or the "Server Side Public License, v 1".
 */

import org.elasticsearch.gradle.Version
import org.elasticsearch.gradle.testclusters.StandaloneRestIntegTestTask

apply plugin: 'elasticsearch.internal-java-rest-test'
apply plugin: 'elasticsearch.internal-test-artifact-base'
apply plugin: 'elasticsearch.bwc-test'
apply plugin: 'elasticsearch.fwc-test'

testArtifacts {
  registerTestArtifactFromSourceSet(sourceSets.javaRestTest)
}

buildParams.bwcVersions.withWireCompatible { bwcVersion, baseName ->
  tasks.register(bwcTaskName(bwcVersion), StandaloneRestIntegTestTask) {
    usesBwcDistribution(bwcVersion)
    systemProperty("tests.old_cluster_version", bwcVersion)
  }
}

tasks.register("bcUpgradeTest", StandaloneRestIntegTestTask) {
  // We use a phony version here as the real version is provided via `tests.bwc.main.version` system property
  usesBwcDistribution(Version.fromString("0.0.0"))
  systemProperty("tests.old_cluster_version", "0.0.0")
  onlyIf("tests.bwc.main.version system property exists") { System.getProperty("tests.bwc.main.version") != null }
  filter {
    // filter tests initially for quicker iterations
    // TODO remove once expanding the test set to other modules
    includeTestsMatching("org.elasticsearch.upgrades.IndexingIT")
  }
}

tasks.withType(Test).configureEach {
  // CI doesn't like it when there's multiple clusters running at once
  maxParallelForks = 1
}

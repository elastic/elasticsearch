/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.elasticsearch.gradle.MavenFilteringHack

apply plugin: 'elasticsearch.rest-test'

ext.pluginCount = 0
for (Project subproj : project.rootProject.subprojects) {
  if (subproj.path.startsWith(':plugins:')) {
    integTest {
      def bundlePlugin = subproj.tasks.findByName('bundlePlugin')
      String camelName = subproj.name.replaceAll(/-(\w)/) { _, c -> c.toUpperCase() }
      dependsOn bundlePlugin
      cluster {
        plugin "install${camelName.capitalize()}", bundlePlugin.outputs.files
      }
    }
    pluginCount += 1
  }
} 

ext.expansions = [
  'expected.plugin.count': pluginCount
]

processTestResources {
  inputs.properties(expansions)
  MavenFilteringHack.filter(it, expansions)
}


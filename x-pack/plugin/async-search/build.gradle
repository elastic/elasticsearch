evaluationDependsOn(xpackModule('core'))

apply plugin: 'elasticsearch.esplugin'
esplugin {
    name 'x-pack-async-search'
    description 'A module which allows to track the progress of a search asynchronously.'
    classname 'org.elasticsearch.xpack.search.AsyncSearch'
    extendedPlugins = ['x-pack-core']
}
archivesBaseName = 'x-pack-async-search'

compileJava.options.compilerArgs << "-Xlint:-rawtypes"
compileTestJava.options.compilerArgs << "-Xlint:-rawtypes"

// add all sub-projects of the qa sub-project
gradle.projectsEvaluated {
  project.subprojects
    .find { it.path == project.path + ":qa" }
    .subprojects
    .findAll { it.path.startsWith(project.path + ":qa") }
    .each { check.dependsOn it.check }
}

dependencies {
  compileOnly project(":server")

  compileOnly project(path: xpackModule('core'), configuration: 'default')
  testCompile project(path: xpackModule('core'), configuration: 'testArtifacts')
  testCompile project(path: xpackModule('ilm'))
}

dependencyLicenses {
  ignoreSha 'x-pack-core'
}

integTest.enabled = false

// Instead we create a separate task to run the
// tests based on ESIntegTestCase
task internalClusterTest(type: Test) {
  description = 'Java fantasy integration tests'
  mustRunAfter test

  include '**/*IT.class'
}

check.dependsOn internalClusterTest

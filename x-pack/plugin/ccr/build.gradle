apply plugin: 'elasticsearch.internal-es-plugin'
apply plugin: 'elasticsearch.internal-cluster-test'
esplugin {
  name 'x-pack-ccr'
  description 'Elasticsearch Expanded Pack Plugin - CCR'
  classname 'org.elasticsearch.xpack.ccr.Ccr'
  hasNativeController false
  requiresKeystore true
  extendedPlugins = ['x-pack-core']
}
archivesBaseName = 'x-pack-ccr'

// Integration Test classes that cannot run with the security manager
String[] noSecurityManagerITClasses = ["**/CloseFollowerIndexIT.class"]

tasks.register('internalClusterTestNoSecurityManager', Test) {
  testClassesDirs = sourceSets.internalClusterTest.output.classesDirs
  classpath = sourceSets.internalClusterTest.runtimeClasspath
  include noSecurityManagerITClasses
  systemProperty 'tests.security.manager', 'false'
}
tasks.named("check").configure { dependsOn 'internalClusterTestNoSecurityManager' }

tasks.named('internalClusterTest').configure {
  exclude noSecurityManagerITClasses
}

addQaCheckDependencies()

dependencies {
  compileOnly project(":server")

  compileOnly project(path: xpackModule('core'))
  testImplementation(testArtifact(project(xpackModule('core'))))
  testImplementation(testArtifact(project(xpackModule('monitoring'))))
  testImplementation(project(":modules:analysis-common"))
  testImplementation(project(":modules:data-streams"))
}

tasks.named("testingConventions").configure {
  naming {
    IT {
      baseClass "org.elasticsearch.xpack.CcrIntegTestCase"
    }
  }
}

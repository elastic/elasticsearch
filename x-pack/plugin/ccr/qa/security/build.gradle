import org.elasticsearch.gradle.internal.test.RestIntegTestTask

apply plugin: 'elasticsearch.internal-testclusters'
apply plugin: 'elasticsearch.standalone-rest-test'

dependencies {
  testImplementation(testArtifact(project(xpackModule('core'))))
  testImplementation project(path: xpackModule('ccr'))
  testImplementation project(':x-pack:plugin:ccr:qa')
}

tasks.register("resolve") {
  doLast {
    configurations.testCompileClasspath.files.each {
      println it
    }
    println "configurations.testCompileClasspath.files " + configurations.testCompileClasspath.files.size()
  }
}

def leadCluster = testClusters.register('leader-cluster') {
    testDistribution = 'DEFAULT'
    setting 'xpack.license.self_generated.type', 'trial'
    setting 'xpack.security.enabled', 'true'
    extraConfigFile 'roles.yml', file('leader-roles.yml')
    user username: "test_admin", role: "superuser"
    user username: "test_ccr", role: "ccruser"
}

def followCluster = testClusters.register('follow-cluster') {
    testDistribution = 'DEFAULT'
    setting 'cluster.remote.leader_cluster.seeds', {
      "\"${leadCluster.get().getAllTransportPortURI().join(",")}\""
    }
    setting 'xpack.license.self_generated.type', 'trial'
    setting 'xpack.security.enabled', 'true'
    setting 'xpack.monitoring.collection.enabled', 'true'
    extraConfigFile 'roles.yml', file('follower-roles.yml')
    user username: "test_admin", role: "superuser"
    user username: "test_ccr", role: "ccruser"
}

tasks.register('leader-cluster', RestIntegTestTask) {
  mustRunAfter("precommit")
  systemProperty 'tests.target_cluster', 'leader'
}

tasks.register('follow-cluster', RestIntegTestTask) {
  dependsOn 'leader-cluster'
  useCluster leadCluster
  systemProperty 'tests.target_cluster', 'follow'
  nonInputProperties.systemProperty 'tests.leader_host', leadCluster.map(c-> c.getAllHttpSocketURI().get(0))
}

tasks.named("check").configure { dependsOn('follow-cluster') }

import org.elasticsearch.gradle.info.BuildParams

evaluationDependsOn(xpackModule('core'))

apply plugin: 'elasticsearch.esplugin'
apply plugin: 'elasticsearch.internal-cluster-test'
esplugin {
  name 'x-pack-data-streams'
  description 'Elasticsearch Expanded Pack Plugin - Data Streams'
  classname 'org.elasticsearch.xpack.datastreams.DataStreamsPlugin'
  extendedPlugins = ['x-pack-core']
}
archivesBaseName = 'x-pack-data-streams'
integTest.enabled = false

tasks.named('internalClusterTest').configure {
  if (BuildParams.isSnapshotBuild() == false) {
    systemProperty 'es.datastreams_feature_enabled', 'true'
  }
}

dependencies {
  compileOnly project(path: xpackModule('core'), configuration: 'default')
  testImplementation project(path: xpackModule('core'), configuration: 'testArtifacts')
}

// add all sub-projects of the qa sub-project
gradle.projectsEvaluated {
  project.subprojects
    .find { it.path == project.path + ":qa" }
    .subprojects
    .findAll { it.path.startsWith(project.path + ":qa") }
    .each { check.dependsOn it.check }
}

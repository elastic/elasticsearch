simple
from employees  | sort emp_no | limit 1 | eval x = to_string(languages) | enrich languages_policy on x  | keep emp_no, language_name;

emp_no:integer | language_name:keyword
10001          | French
;


simple2
from employees | eval x = to_string(languages) | enrich languages_policy on x  | keep emp_no, language_name | sort emp_no | limit 1 ;

emp_no:integer | language_name:keyword
10001          | French
;

simpleSortLimit
from employees  | eval x = to_string(languages) | enrich languages_policy on x  | keep emp_no, language_name | sort emp_no | limit 1;

emp_no:integer | language_name:keyword
10001          | French
;


with
from employees | eval x = to_string(languages) | keep emp_no, x | sort emp_no | limit 1 
| enrich languages_policy on x with language_name;

emp_no:integer | x:keyword | language_name:keyword
10001          | 2         | French
;


withAlias
from employees  | sort emp_no | limit 3 | eval x = to_string(languages) | keep emp_no, x 
| enrich languages_policy on x with lang = language_name;

emp_no:integer | x:keyword | lang:keyword
10001          | 2         | French
10002          | 5         | null
10003          | 4         | German
;



withAliasSort
from employees | eval x = to_string(languages) | keep emp_no, x  | sort emp_no | limit 3 
| enrich languages_policy on x with lang = language_name;

emp_no:integer | x:keyword | lang:keyword
10001          | 2         | French
10002          | 5         | null
10003          | 4         | German
;


withAliasAndPlain
from employees  | sort emp_no desc | limit 3 | eval x = to_string(languages) | keep emp_no, x 
| enrich languages_policy on x with lang = language_name, language_name;

emp_no:integer | x:keyword | lang:keyword | language_name:keyword
10100          | 4         | German       | German
10099          | 2         | French       | French
10098          | 4         | German       | German
;


withTwoAliasesSameProp
from employees  | sort emp_no | limit 1 | eval x = to_string(languages) | keep emp_no, x 
| enrich languages_policy on x with lang = language_name, lang2 = language_name;

emp_no:integer | x:keyword | lang:keyword | lang2:keyword
10001          | 2         | French       | French
;


redundantWith
from employees  | sort emp_no | limit 1 | eval x = to_string(languages) | keep emp_no, x 
| enrich languages_policy on x with language_name, language_name;

emp_no:integer | x:keyword | language_name:keyword 
10001          | 2         | French
;


nullInput
from employees  | where emp_no == 10017 | keep emp_no, gender
| enrich languages_policy on gender with language_name, language_name;

emp_no:integer | gender:keyword | language_name:keyword 
10017          | null           | null
;

 
constantNullInput
from employees  | where emp_no == 10020 | eval x = to_string(languages) | keep emp_no, x 
| enrich languages_policy on x with language_name, language_name;

emp_no:integer | x:keyword | language_name:keyword 
10020          | null      | null
;


multipleEnrich
row a = "1", b = "2", c = "10"  
| enrich languages_policy on a with a_lang = language_name  
| enrich languages_policy on b with b_lang = language_name  
| enrich languages_policy on c with c_lang = language_name;

a:keyword | b:keyword | c:keyword | a_lang:keyword | b_lang:keyword | c_lang:keyword
1         | 2         | 10        | English        | French         | null
;


enrichEval
from employees | eval x = to_string(languages) 
| enrich languages_policy on x with lang = language_name
| eval language = concat(x, "-", lang)
| keep emp_no, x, lang, language 
| sort emp_no desc | limit 3;

emp_no:integer | x:keyword | lang:keyword | language:keyword
10100          | 4         | German       | 4-German
10099          | 2         | French       | 2-French
10098          | 4         | German       | 4-German
;


multivalue
row a = ["1", "2"] | enrich languages_policy on a with a_lang = language_name;  

a:keyword   | a_lang:keyword
["1", "2"]  | ["English", "French"] 
;

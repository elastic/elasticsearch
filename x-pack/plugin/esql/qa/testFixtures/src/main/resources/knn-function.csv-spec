# TODO Most tests explicitly set k. Until knn function uses LIMIT as k, we need to explicitly set it to all values
# in the dataset to avoid test failures due to docs allocation in different shards, which can impact results for a 
# top-n query at the shard level 

knnSearch
required_capability: knn_function_v3

// tag::knn-function[]
from colors metadata _score 
| where knn(rgb_vector, [0, 120, 0], 10) 
| sort _score desc, color asc
// end::knn-function[]
| keep color, rgb_vector
| limit 10
;

// tag::knn-function-result[]
color:text | rgb_vector:dense_vector
green      | [0.0, 128.0, 0.0]
black      | [0.0, 0.0, 0.0]
olive      | [128.0, 128.0, 0.0]
teal       | [0.0, 128.0, 128.0]
lime       | [0.0, 255.0, 0.0]
sienna     | [160.0, 82.0, 45.0]
maroon     | [128.0, 0.0, 0.0]
navy       | [0.0, 0.0, 128.0]
gray       | [128.0, 128.0, 128.0]
chartreuse | [127.0, 255.0, 0.0]
// end::knn-function-result[]
;

knnSearchWithSimilarityOption
required_capability: knn_function_v3

from colors metadata _score 
| where knn(rgb_vector, [255,192,203], 140, {"similarity": 40})
| sort _score desc, color asc
| keep color, rgb_vector
;

color:text | rgb_vector:dense_vector
pink       | [255.0, 192.0, 203.0]
peach puff | [255.0, 218.0, 185.0]
bisque     | [255.0, 228.0, 196.0]
wheat      | [245.0, 222.0, 179.0]
;

knnHybridSearch
required_capability: knn_function_v3

from colors metadata _score 
| where match(color, "blue") or knn(rgb_vector, [65,105,225], 10)
| where primary == true
| sort _score desc, color asc
| keep color, rgb_vector
;

color:text | rgb_vector:dense_vector
blue       | [0.0, 0.0, 255.0]
gray       | [128.0, 128.0, 128.0]
cyan       | [0.0, 255.0, 255.0]
magenta    | [255.0, 0.0, 255.0]
green      | [0.0, 128.0, 0.0]
white      | [255.0, 255.0, 255.0]
black      | [0.0, 0.0, 0.0]
red        | [255.0, 0.0, 0.0]
yellow     | [255.0, 255.0, 0.0]
;

knnWithPrefilter
required_capability: knn_function_v3

from colors
| where knn(rgb_vector, [120,180,0], 10) and (match(color, "olive") or match(color, "green")) 
| sort color asc
| keep color
;

color:text
green
olive
;

knnWithNegatedPrefilter
required_capability: knn_function_v3

from colors metadata _score
| where knn(rgb_vector, [128,128,0], 10) and not (match(color, "olive") or match(color, "chocolate")) 
| sort _score desc, color asc
| keep color, rgb_vector
| LIMIT 10
;

color:text | rgb_vector:dense_vector
sienna     | [160.0, 82.0, 45.0]
peru       | [205.0, 133.0, 63.0]
golden rod | [218.0, 165.0, 32.0]
brown      | [165.0, 42.0, 42.0]
firebrick  | [178.0, 34.0, 34.0]
chartreuse | [127.0, 255.0, 0.0]
gray       | [128.0, 128.0, 128.0]
green      | [0.0, 128.0, 0.0]
maroon     | [128.0, 0.0, 0.0]
orange     | [255.0, 165.0, 0.0]
;

knnAfterKeep
required_capability: knn_function_v3

from colors metadata _score
| keep rgb_vector, color, _score 
| where knn(rgb_vector, [128,255,0], 140)
| sort _score desc, color asc
| keep rgb_vector
| limit 5
;

rgb_vector:dense_vector
[127.0, 255.0, 0.0]
[128.0, 128.0, 0.0]
[255.0, 255.0, 0.0]
[0.0, 255.0, 0.0]
[218.0, 165.0, 32.0]
;

knnAfterDrop
required_capability: knn_function_v3

from colors metadata _score
| drop primary
| where knn(rgb_vector, [128,250,0], 140)
| sort _score desc, color asc
| keep color, rgb_vector
| limit 5
;

color:text     | rgb_vector: dense_vector       
chartreuse     | [127.0, 255.0, 0.0]  
olive          | [128.0, 128.0, 0.0] 
yellow         | [255.0, 255.0, 0.0] 
golden rod     | [218.0, 165.0, 32.0]
lime           | [0.0, 255.0, 0.0] 
;

knnAfterEval
required_capability: knn_function_v3

from colors metadata _score
| eval composed_name = locate(color, " ") > 0 
| where knn(rgb_vector, [128,128,0], 140)
| sort _score desc, color asc
| keep color, composed_name 
| limit 5
;

color:text | composed_name:boolean
olive      | false
sienna     | false
chocolate  | false
peru       | false
golden rod | true
;

knnWithConjunction
required_capability: knn_function_v3

from colors metadata _score 
| where knn(rgb_vector, [255,255,238], 10) and hex_code like "#FFF*" 
| sort _score desc, color asc
| keep color, hex_code, rgb_vector
;

color:text    | hex_code:keyword | rgb_vector:dense_vector
ivory         | #FFFFF0          | [255.0, 255.0, 240.0]
sea shell     | #FFF5EE          | [255.0, 245.0, 238.0]
snow          | #FFFAFA          | [255.0, 250.0, 250.0]
white         | #FFFFFF          | [255.0, 255.0, 255.0]
corn silk     | #FFF8DC          | [255.0, 248.0, 220.0]
lemon chiffon | #FFFACD          | [255.0, 250.0, 205.0]
yellow        | #FFFF00          | [255.0, 255.0, 0.0]
;

knnWithDisjunctionAndFiltersConjunction
required_capability: knn_function_v3

from colors metadata _score 
| where (knn(rgb_vector, [0,255,255], 140) or knn(rgb_vector, [128, 0, 255], 10)) and primary == true 
| keep color, rgb_vector, _score
| sort _score desc, color asc
| drop _score
| limit 10
;

color:text | rgb_vector:dense_vector
cyan       | [0.0, 255.0, 255.0]
blue       | [0.0, 0.0, 255.0]
magenta    | [255.0, 0.0, 255.0]
gray       | [128.0, 128.0, 128.0]
white      | [255.0, 255.0, 255.0]
green      | [0.0, 128.0, 0.0]
black      | [0.0, 0.0, 0.0]
red        | [255.0, 0.0, 0.0]
yellow     | [255.0, 255.0, 0.0]
;

knnWithNegationsAndFiltersConjunction
required_capability: knn_function_v3

from colors metadata _score 
| where (knn(rgb_vector, [0,255,255], 140) and not(primary == true and match(color, "blue"))) 
| sort _score desc, color asc
| keep color, rgb_vector
| limit 10
;

color:text  | rgb_vector:dense_vector
cyan        | [0.0, 255.0, 255.0]
turquoise   | [64.0, 224.0, 208.0]
aqua marine | [127.0, 255.0, 212.0]
teal        | [0.0, 128.0, 128.0]
silver      | [192.0, 192.0, 192.0]
gray        | [128.0, 128.0, 128.0]
gainsboro   | [220.0, 220.0, 220.0]
thistle     | [216.0, 191.0, 216.0]
lavender    | [230.0, 230.0, 250.0]
azure       | [240.0, 255.0, 255.0]
;

knnWithNonPushableConjunction
required_capability: knn_function_v3

from colors metadata _score
| eval composed_name = locate(color, " ") > 0 
| where knn(rgb_vector, [128,128,0], 140) and composed_name == false
| sort _score desc, color asc
| keep color, composed_name
| limit 10
;

color:text | composed_name:boolean
olive      | false
sienna     | false
chocolate  | false
peru       | false
brown      | false
firebrick  | false
chartreuse | false
gray       | false
green      | false
maroon     | false
;

testKnnWithNonPushableDisjunctions
required_capability: knn_function_v3

from colors metadata _score 
| where knn(rgb_vector, [128,128,0], 140, {"similarity": 30}) or length(color) > 10 
| sort _score desc, color asc
| keep color 
;

color:text
olive
aqua marine
lemon chiffon
papaya whip
;

testKnnWithNonPushableDisjunctionsOnComplexExpressions
required_capability: knn_function_v3

from colors metadata _score 
| where (knn(rgb_vector, [128,128,0], 140, {"similarity": 70}) and length(color) < 10) or (knn(rgb_vector, [128,0,128], 140, {"similarity": 60}) and primary == false) 
| sort _score desc, color asc
| keep color, primary
;

color:text   | primary:boolean
olive        | false
purple       | false
indigo       | false
;

testKnnInStatsNonPushable
required_capability: knn_function_v3

from colors 
| where length(color) < 10 
| stats c = count(*) where knn(rgb_vector, [128,128,255], 140)
;

c: long 
50      
;

testKnnInStatsWithGrouping
required_capability: knn_function_v3
required_capability: full_text_functions_in_stats_where

from colors 
| where length(color) < 10 
| stats c = count(*) where knn(rgb_vector, [128,128,255], 140) by primary
;

c: long       | primary: boolean    
41            | false          
9             | true           
;

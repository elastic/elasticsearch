###############################################
# Tests for QSTR function
#

qstrWithField
required_capability: qstr_function

// tag::qstr-with-field[]
from books 
| where qstr("author: Faulkner")
| keep book_no, author 
| sort book_no 
| limit 5;
// end::qstr-with-field[]

// tag::qstr-with-field-result[]
book_no:keyword | author:text
2378            | [Carol Faulkner, Holly Byers Ochoa, Lucretia Mott]
2713            | William Faulkner
2847            | Colleen Faulkner
2883            | William Faulkner
3293            | Danny Faulkner
;
// end::qstr-with-field-result[]

qstrWithMultipleFields
required_capability: qstr_function

from books 
| where qstr("title:Return* AND author:*Tolkien")  
| keep book_no, title;
ignoreOrder:true

book_no:keyword | title:text
2714            | Return of the King Being the Third Part of The Lord of the Rings
7350            | Return of the Shadow
;

qstrWithQueryExpressions
required_capability: qstr_function

from books 
| where qstr(CONCAT("title:Return*", " AND author:*Tolkien"))  
| keep book_no, title;
ignoreOrder:true

book_no:keyword | title:text
2714            | Return of the King Being the Third Part of The Lord of the Rings
7350            | Return of the Shadow
;

qstrWithPushableDisjunction
required_capability: qstr_function

from books 
| where qstr("title:Return") or book_no == "7140" or year > 2020
| keep book_no, title;
ignoreOrder:true

book_no:keyword | title:text
2714            | Return of the King Being the Third Part of The Lord of the Rings
7350            | Return of the Shadow         
7140            | The Lord of the Rings Poster Collection: Six Paintings by Alan Lee (No. 1)                                   
6818            | Hadji Murad                  
;

qstrWithConjunction
required_capability: qstr_function

from books 
| where qstr("title: Rings") and ratings > 4.6
| keep book_no, title;
ignoreOrder:true

book_no:keyword | title:text
4023            |A Tolkien Compass: Including J. R. R. Tolkien's Guide to the Names in The Lord of the Rings
7140            |The Lord of the Rings Poster Collection: Six Paintings by Alan Lee (No. 1)     
;

qstrWithFunctionPushedToLucene
required_capability: qstr_function

from hosts 
| where qstr("host: beta") and cidr_match(ip1, "127.0.0.2/32", "127.0.0.3/32") 
| keep card, host, ip0, ip1;
ignoreOrder:true

card:keyword   |host:keyword   |ip0:ip                   |ip1:ip
eth1           |beta           |127.0.0.1                |127.0.0.2
;

qstrWithNonPushableConjunction
required_capability: qstr_function

from books 
| where qstr("title: Rings") and length(title) > 75
| keep book_no, title;
ignoreOrder:true

book_no:keyword | title:text
4023            |A Tolkien Compass: Including J. R. R. Tolkien's Guide to the Names in The Lord of the Rings
;

qstrWithMultipleWhereClauses
required_capability: qstr_function

from books 
| where qstr("title: rings") 
| where qstr("year: [1 TO 2005]") 
| keep book_no, title;
ignoreOrder:true

book_no:keyword | title:text
4023            | A Tolkien Compass: Including J. R. R. Tolkien's Guide to the Names in The Lord of the Rings           
7140            | The Lord of the Rings Poster Collection: Six Paintings by Alan Lee (No. 1)
;

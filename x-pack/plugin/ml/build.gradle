import org.elasticsearch.gradle.VersionProperties

apply plugin: 'elasticsearch.internal-es-plugin'
apply plugin: 'elasticsearch.internal-cluster-test'
apply plugin: 'elasticsearch.internal-test-artifact'
apply plugin: 'elasticsearch.dra-artifacts'

esplugin {
  name 'x-pack-ml'
  description 'Elasticsearch Expanded Pack Plugin - Machine Learning'
  classname 'org.elasticsearch.xpack.ml.MachineLearning'
  hasNativeController true
  extendedPlugins = ['x-pack-autoscaling', 'lang-painless']
}

def localRepo = providers.systemProperty('build.ml_cpp.repo').orNull

if (useDra == false) {
  repositories {
    exclusiveContent {
      filter {
        includeGroup 'org.elasticsearch.ml'
      }
      forRepository {
        ivy {
          name "ml-cpp"
          metadataSources {
            // no repository metadata, look directly for the artifact
            artifact()
          }
          if (localRepo) {
            url localRepo
            patternLayout {
              artifact "maven/[orgPath]/[module]/[revision]/[module]-[revision](-[classifier]).[ext]"
            }
          } else {
            url "https://artifacts-snapshot.elastic.co/"
            patternLayout {
              if (VersionProperties.isElasticsearchSnapshot()) {
                artifact '/ml-cpp/[revision]/downloads/ml-cpp/[module]-[revision](-[classifier]).[ext]'
              } else {
                // When building locally we always use snapshot artifacts even if passing `-Dbuild.snapshot=false`.
                // Release builds are always done with a local repo.
                artifact '/ml-cpp/[revision]-SNAPSHOT/downloads/ml-cpp/[module]-[revision]-SNAPSHOT(-[classifier]).[ext]'
              }
            }
          }
        }
      }
    }
  }
}
configurations {
  nativeBundle {
    resolutionStrategy.dependencySubstitution {
      if (findProject(':ml-cpp') != null) {
        substitute module("org.elasticsearch.ml:ml-cpp") with project(":ml-cpp")
      }
    }
    resolutionStrategy.cacheChangingModulesFor 2, 'hours'
  }
}

tasks.named("bundlePlugin").configure {
  dependsOn configurations.nativeBundle
  from {
    project.zipTree(configurations.nativeBundle.singleFile)
  }

  // We don't ship the individual nativeBundle licenses - instead
  // they get combined into the top level NOTICES file we ship
  exclude 'platform/licenses/**'
}

dependencies {
  compileOnly project(':modules:lang-painless:spi')
  compileOnly project(path: xpackModule('core'))
  compileOnly project(path: xpackModule('autoscaling'))
  testImplementation(testArtifact(project(xpackModule('core'))))
  testImplementation project(path: xpackModule('ilm'))
  testImplementation project(path: xpackModule('shutdown'))
  testImplementation project(path: xpackModule('data-streams'))
  testImplementation project(path: xpackModule('monitoring'))
  testImplementation project(':modules:ingest-common')
  testImplementation project(':modules:reindex')
  testImplementation project(':modules:analysis-common')
  // This should not be here
  testImplementation(testArtifact(project(xpackModule('security'))))
  // ml deps
  api project(':libs:elasticsearch-grok')
  api "net.sf.supercsv:super-csv:${versions.supercsv}"
  api "org.apache.commons:commons-math3:3.6.1"
  nativeBundle("org.elasticsearch.ml:ml-cpp:${project.version}@zip") {
    changing = true
  }
  testImplementation 'org.ini4j:ini4j:0.5.2'
}

artifacts {
  // normal es plugins do not publish the jar but we need to since users need it for extensions
  archives tasks.named("jar")
}

tasks.register("extractNativeLicenses", Copy) {
  dependsOn configurations.nativeBundle
  into "${buildDir}/extractedNativeLicenses"
  from {
    project.zipTree(configurations.nativeBundle.singleFile)
  }
  include 'platform/licenses/**'
}
project.afterEvaluate {
  // Add an extra licenses directory to the combined notices
  tasks.named('generateNotice').configure {
    dependsOn "extractNativeLicenses"
    licensesDir new File("${project.buildDir}/extractedNativeLicenses/platform/licenses")
    outputs.upToDateWhen {
      extractNativeLicenses.didWork
    }
  }
}

addQaCheckDependencies()

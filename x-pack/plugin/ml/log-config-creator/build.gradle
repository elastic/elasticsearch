apply plugin: 'elasticsearch.build'

/* We don't use the 'application' plugin because it builds a zip and tgz which
 * we don't want. */

archivesBaseName = 'log-config-creator'

description = 'Create log file import configurations'

dependencies {
    compile project(path: xpackModule('core'))
    compile project(path: xpackModule('ml'))
    compile "org.elasticsearch:elasticsearch:${version}"
    compile "org.elasticsearch:elasticsearch-cli:${version}"
    compile 'joda-time:joda-time:2.10'
    compile "com.fasterxml.jackson.core:jackson-core:${versions.jackson}"
    compile "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
    compile "com.fasterxml.jackson.core:jackson-annotations:${versions.jackson}"

    testCompile "org.elasticsearch.test:framework:${version}"
    testCompile project(path: xpackModule('ml'), configuration: 'testArtifacts')
}

dependencyLicenses.enabled = false

/*
 * Bundle all dependencies into the main jar and mark it as executable it
 * can be easilly shipped around and used.
 */
jar {
    from({
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    }) {
        // We don't need the META-INF from the things we bundle. For now.
        exclude 'META-INF/*'
    }
    manifest {
        attributes 'Main-Class': 'org.elasticsearch.xpack.ml.configcreator.LogConfigCreator'
    }
}

forbiddenApisMain {
    // log-config-creator does not depend on server, so cannot forbid server methods
    replaceSignatureFiles 'jdk-signatures'
}


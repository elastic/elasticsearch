apply plugin: 'elasticsearch.internal-es-plugin'
apply plugin: 'elasticsearch.internal-cluster-test'
apply plugin: 'elasticsearch.internal-test-artifact'

esplugin {
  name 'x-pack-monitoring'
  description 'Elasticsearch Expanded Pack Plugin - Monitoring'
  classname 'org.elasticsearch.xpack.monitoring.Monitoring'
  extendedPlugins = ['x-pack-core']
}
base {
  archivesName = 'x-pack-monitoring'
}

dependencies {
  compileOnly project(path: xpackModule('core'))

  // monitoring deps
  api project(':client:rest')
  api project(':client:sniffer')

  // baz - this goes away after we separate out the actions #27759
  testImplementation project(":modules:analysis-common")
  testImplementation project(xpackModule('watcher'))
  testImplementation project(xpackModule('ilm'))
  testImplementation project(xpackModule('data-streams'))
  testImplementation(testArtifact(project(xpackModule('core'))))
}


artifacts {
  // normal es plugins do not publish the jar but we need to since users need it for Transport Clients and extensions
  archives tasks.named("jar")
}

tasks.named("dependencyLicenses").configure {
  mapping from: /http.*/, to: 'httpclient' // pulled in by rest client
  mapping from: /commons-.*/, to: 'commons' // pulled in by rest client
}

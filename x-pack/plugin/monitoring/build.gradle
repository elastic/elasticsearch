evaluationDependsOn(xpackModule('core'))

apply plugin: 'elasticsearch.esplugin'
apply plugin: 'elasticsearch.internal-cluster-test'
esplugin {
  name 'x-pack-monitoring'
  description 'Elasticsearch Expanded Pack Plugin - Monitoring'
  classname 'org.elasticsearch.xpack.monitoring.Monitoring'
  extendedPlugins = ['x-pack-core']
}
archivesBaseName = 'x-pack-monitoring'

dependencies {
  compileOnly project(path: xpackModule('core'), configuration: 'default')
  testImplementation project(path: xpackModule('core'), configuration: 'testArtifacts')

  // monitoring deps
  compile project(':client:rest')
  compile project(':client:sniffer')

  // baz - this goes away after we separate out the actions #27759
  testImplementation project(xpackModule('watcher'))

  testImplementation project(xpackModule('ilm'))
}

configurations {
  testArtifacts.extendsFrom testRuntime
  testArtifacts.extendsFrom testImplementation
}
task testJar(type: Jar) {
  appendix 'test'
  from sourceSets.test.output
}
artifacts {
  // normal es plugins do not publish the jar but we need to since users need it for extensions
  archives jar
  testArtifacts testJar
}

tasks.named("dependencyLicenses").configure {
  mapping from: /http.*/, to: 'httpclient' // pulled in by rest client
  mapping from: /commons-.*/, to: 'commons' // pulled in by rest client
}

// xpack modules are installed in real clusters as the meta plugin, so
// installing them as individual plugins for integ tests doesn't make sense,
// so we disable integ tests
integTest.enabled = false

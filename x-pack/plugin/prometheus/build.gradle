plugins {
  id 'elasticsearch.internal-es-plugin'
  id 'elasticsearch.internal-yaml-rest-test'
  id 'elasticsearch.internal-java-rest-test'
  id 'elasticsearch.internal-cluster-test'
  id('com.google.protobuf') version '0.9.6'
}

esplugin {
  name = 'x-pack-prometheus'
  description = 'The Prometheus plugin defines Prometheus mappings and contains Prometheus-compatible APIs.'
  classname ='org.elasticsearch.xpack.prometheus.PrometheusPlugin'
  extendedPlugins = ['x-pack-core']
}

def protobufVersion = "4.32.0"

dependencies {
  compileOnly project(path: xpackModule('core'))
  testImplementation(testArtifact(project(xpackModule('core'))))
  // stack is needed as we import the metrics@custom template
  clusterModules project(xpackModule('stack'))
  // transitive dependencies of stack
  clusterModules project(xpackModule('wildcard'))
  clusterModules project(xpackModule('ilm'))
  clusterModules project(xpackModule('mapper-constant-keyword'))
  clusterModules project(':modules:ingest-common')
  clusterModules project(':modules:mapper-extras')
  clusterModules project(':modules:data-streams')

  api "com.google.protobuf:protobuf-java:${protobufVersion}"
  javaRestTestImplementation "com.google.protobuf:protobuf-java:${protobufVersion}"
  javaRestTestImplementation sourceSets.main.output
  // The protobuf plugin only adds a dependency for the variant relevant for the current platform.
  // Without explicitly adding all classifiers, the --write-verification-metadata task would only add the one for the current platform.
  // Therefore, the verification check would fail on other platforms, like on CI or for contributors that work on another platform.
  testRuntimeOnly "com.google.protobuf:protoc:${protobufVersion}:linux-aarch_64@exe"
  testRuntimeOnly "com.google.protobuf:protoc:${protobufVersion}:linux-ppcle_64@exe"
  testRuntimeOnly "com.google.protobuf:protoc:${protobufVersion}:linux-s390_64@exe"
  testRuntimeOnly "com.google.protobuf:protoc:${protobufVersion}:linux-x86_32@exe"
  testRuntimeOnly "com.google.protobuf:protoc:${protobufVersion}:linux-x86_64@exe"
  testRuntimeOnly "com.google.protobuf:protoc:${protobufVersion}:osx-aarch_64@exe"
  testRuntimeOnly "com.google.protobuf:protoc:${protobufVersion}:osx-universal_binary@exe"
  testRuntimeOnly "com.google.protobuf:protoc:${protobufVersion}:osx-x86_64@exe"
  testRuntimeOnly "com.google.protobuf:protoc:${protobufVersion}:windows-x86_32@exe"
  testRuntimeOnly "com.google.protobuf:protoc:${protobufVersion}:windows-x86_64@exe"
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${protobufVersion}"
  }
}

idea {
  module {
    sourceDirs += layout.buildDirectory.dir("generated/sources/proto/main/java").get().asFile
  }
}

tasks.named("dependencyLicenses").configure {
  mapping from: /protobuf.*/, to: 'protobuf'
}

tasks.named("thirdPartyAudit").configure {
  ignoreViolations(
    // uses internal java api: sun.misc.Unsafe
    'com.google.protobuf.MessageSchema',
    'com.google.protobuf.UnsafeUtil',
    'com.google.protobuf.UnsafeUtil$1',
    'com.google.protobuf.UnsafeUtil$Android32MemoryAccessor',
    'com.google.protobuf.UnsafeUtil$Android64MemoryAccessor',
    'com.google.protobuf.UnsafeUtil$JvmMemoryAccessor',
    'com.google.protobuf.UnsafeUtil$MemoryAccessor'
  )
}

tasks.named("licenseHeaders").configure {
  excludes << 'org/elasticsearch/xpack/prometheus/proto/**/*'
}

tasks.named('yamlRestTest') {
  usesDefaultDistribution("Requires a bunch of xpack plugins")
}

tasks.named('javaRestTest').configure {
  usesDefaultDistribution("Prometheus REST tests require full distribution")
}


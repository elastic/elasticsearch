import com.carrotsearch.gradle.junit4.RandomizedTestingTask
import org.elasticsearch.gradle.BuildPlugin

evaluationDependsOn(xpackModule('core'))

apply plugin: 'elasticsearch.esplugin'
esplugin {
    name 'x-pack-rollup'
    description 'Elasticsearch Expanded Pack Plugin - Rollup'
    classname 'org.elasticsearch.xpack.rollup.Rollup'
    extendedPlugins = ['x-pack-core']
}
archivesBaseName = 'x-pack-rollup'

compileJava.options.compilerArgs << "-Xlint:-rawtypes"
compileTestJava.options.compilerArgs << "-Xlint:-rawtypes"


dependencies {
    compileOnly "org.elasticsearch:elasticsearch:${version}"

    compileOnly "org.elasticsearch.plugin:x-pack-core:${version}"
    testCompile project(path: xpackModule('core'), configuration: 'testArtifacts')
}

dependencyLicenses {
    ignoreSha 'x-pack-core'
}

run {
    plugin xpackModule('core')
}

integTest.enabled = false


// Instead we create a separate task to run the
// tests based on ESIntegTestCase
task internalClusterTest(type: RandomizedTestingTask,
        group: JavaBasePlugin.VERIFICATION_GROUP,
        description: 'Multi-node tests',
        dependsOn: test.dependsOn) {
    configure(BuildPlugin.commonTestConfig(project))
    classpath = project.test.classpath
    testClassesDirs = project.test.testClassesDirs
    include '**/*IT.class'
    systemProperty 'es.set.netty.runtime.available.processors', 'false'
}
check.dependsOn internalClusterTest
internalClusterTest.mustRunAfter test

// also add an "alias" task to make typing on the command line easier task icTest {
task icTest {
    dependsOn internalClusterTest
}

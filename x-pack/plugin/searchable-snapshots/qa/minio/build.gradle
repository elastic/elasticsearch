import static org.elasticsearch.gradle.PropertyNormalization.IGNORE_VALUE

apply plugin: 'elasticsearch.legacy-java-rest-test'
apply plugin: 'elasticsearch.test.fixtures'
apply plugin: 'elasticsearch.rest-resources'

final Project fixture = project(':test:fixtures:minio-fixture')
final Project repositoryPlugin = project(':plugins:repository-s3')

dependencies {
  javaRestTestImplementation(testArtifact(project(xpackModule('searchable-snapshots'))))
  javaRestTestImplementation repositoryPlugin
}

restResources {
  restApi {
    include 'indices', 'search', 'bulk', 'snapshot', 'nodes', '_common', 'searchable_snapshots'
  }
}

testFixtures.useFixture(fixture.path, 'minio-fixture-other')
def fixtureAddress = {
  int ephemeralPort =  fixture.postProcessFixture.ext."test.fixtures.minio-fixture-other.tcp.9000"
  assert ephemeralPort > 0
  '127.0.0.1:' + ephemeralPort
}

tasks.named("javaRestTest").configure {
  systemProperty 'test.minio.bucket', 'bucket'
  systemProperty 'test.minio.base_path', 'searchable_snapshots_tests'
}

testClusters.matching { it.name == "javaRestTest" }.configureEach {
  testDistribution = 'DEFAULT'
  plugin repositoryPlugin.path

  keystore 's3.client.searchable_snapshots.access_key', 's3_test_access_key'
  keystore 's3.client.searchable_snapshots.secret_key', 's3_test_secret_key'
  setting 'xpack.license.self_generated.type', 'trial'
  setting 's3.client.searchable_snapshots.protocol', 'http'
  setting 's3.client.searchable_snapshots.endpoint', { "${-> fixtureAddress()}" }, IGNORE_VALUE

  setting 'xpack.searchable.snapshot.shared_cache.size', '16MB'
  setting 'xpack.searchable.snapshot.shared_cache.region_size', '256KB'
  setting 'xpack.searchable_snapshots.cache_fetch_async_thread_pool.keep_alive', '0ms'

  setting 'xpack.security.enabled', 'false'
}

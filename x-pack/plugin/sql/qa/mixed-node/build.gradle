apply plugin: 'elasticsearch.legacy-java-rest-test'
apply plugin: 'elasticsearch.bwc-test'

import org.elasticsearch.gradle.VersionProperties
import org.elasticsearch.gradle.internal.info.BuildParams
import org.elasticsearch.gradle.testclusters.StandaloneRestIntegTestTask

dependencies {
  javaRestTestImplementation project(':x-pack:qa')
  javaRestTestImplementation(project(xpackModule('ql:test-fixtures')))
  javaRestTestImplementation project(xpackModule('sql'))
  javaRestTestImplementation project(xpackModule('sql:qa:server'))
}

testClusters.configureEach {
  setting 'xpack.security.enabled', 'false'
}

tasks.named("javaRestTest").configure{ enabled = false}

// A bug (https://github.com/elastic/elasticsearch/issues/68439) limits us to perform tests with versions from 7.10.3 onwards
BuildParams.bwcVersions.withWireCompatible(v -> v.onOrAfter("7.10.3") &&
        v != VersionProperties.getElasticsearchVersion()) { bwcVersion, baseName ->

  def baseCluster = testClusters.register(baseName) {
      versions = [bwcVersion.toString(), project.version]
      numberOfNodes = 3
      testDistribution = 'DEFAULT'
      setting 'xpack.security.enabled', 'false'
      setting 'xpack.watcher.enabled', 'false'
      setting 'xpack.ml.enabled', 'false'
      setting 'xpack.license.self_generated.type', 'trial'
      // for debugging purposes
      // setting 'logger.org.elasticsearch.xpack.sql.plugin.TransportSqlQueryAction', 'TRACE'
  }

  tasks.register("${baseName}#mixedClusterTest", StandaloneRestIntegTestTask) {
      useCluster baseCluster
      mustRunAfter("precommit")
      testClassesDirs = sourceSets.javaRestTest.output.classesDirs
      classpath = sourceSets.javaRestTest.runtimeClasspath
      doFirst {
          def cluster = baseCluster.get()
          // Getting the endpoints causes a wait for the cluster
          println "Endpoints are: ${-> cluster.allHttpSocketURI.join(",")}"
          println "Upgrading one node to create a mixed cluster"
          cluster.nextNodeToNextVersion()

          println "Upgrade complete, endpoints are: ${-> cluster.allHttpSocketURI.join(",")}"
          nonInputProperties.systemProperty('tests.rest.cluster', baseCluster.map(c->c.allHttpSocketURI.join(",")))
          nonInputProperties.systemProperty('tests.clustername', baseName)

    }
    onlyIf("BWC tests disabled") { project.bwc_tests_enabled }
  }

  tasks.register(bwcTaskName(bwcVersion)) {
    dependsOn "${baseName}#mixedClusterTest"
  }
}

import org.elasticsearch.gradle.testclusters.DefaultTestClustersTask

apply plugin: 'elasticsearch.legacy-java-rest-test'

dependencies {
  javaRestTestImplementation project(path: xpackModule('ql:test-fixtures'))
}

def remoteClusterReg = testClusters.register('remote-cluster') {
  testDistribution = 'DEFAULT'
  setting 'node.roles', '[data,ingest,master]'
  setting 'xpack.ml.enabled', 'false'
  setting 'xpack.watcher.enabled', 'false'
  setting 'xpack.security.enabled', 'true'
  setting 'xpack.license.self_generated.type', 'trial'
  setting 'xpack.security.autoconfiguration.enabled', 'false'

  user username: "test_user", password: "x-pack-test-password"
  plugin ':x-pack:qa:freeze-plugin'
}

def javaRestTestClusterReg = testClusters.register('javaRestTest') {
  testDistribution = 'DEFAULT'
  setting 'xpack.ml.enabled', 'false'
  setting 'xpack.watcher.enabled', 'false'
  setting 'cluster.remote.my_remote_cluster.seeds', {
    remoteClusterReg.get().getAllTransportPortURI().collect { "\"$it\"" }.toString()
  }
  setting 'cluster.remote.connections_per_cluster', "1"
  setting 'xpack.security.enabled', 'true'
  setting 'xpack.license.self_generated.type', 'trial'
  setting 'xpack.security.autoconfiguration.enabled', 'false'

  user username: "test_user", password: "x-pack-test-password"
  plugin ':x-pack:qa:freeze-plugin'
}

tasks.register("startRemoteCluster", DefaultTestClustersTask.class) {
  useCluster remoteClusterReg
  doLast {
    "Starting remote cluster before integ tests and integ test cluster is started"
  }
}

tasks.named("javaRestTest").configure {
  dependsOn 'startRemoteCluster'
  useCluster remoteClusterReg
  doFirst {
    nonInputProperties.systemProperty 'tests.rest.cluster.remote.host', remoteClusterReg.map(c->c.getAllHttpSocketURI().get(0))
    // credentials for both local and remote clusters
    nonInputProperties.systemProperty 'tests.rest.cluster.multi.user', "test_user"
    nonInputProperties.systemProperty 'tests.rest.cluster.multi.password', "x-pack-test-password"
  }
}
tasks.named("check").configure {dependsOn("javaRestTest") } // run these tests as part of the "check" task

apply plugin: 'elasticsearch.esplugin'
esplugin {
  name 'transform'
  description 'A plugin to transform data'
  classname 'org.elasticsearch.xpack.transform.Transform'
  extendedPlugins = ['x-pack-core']
}

dependencies {
  compileOnly project(":server")

  compileOnly project(path: xpackModule('core'), configuration: 'default')
  testImplementation project(path: xpackModule('core'), configuration: 'testArtifacts')
  testImplementation project(path: xpackModule('analytics'))
  testImplementation project(path: ':modules:aggs-matrix-stats')
}

// xpack modules are installed in real clusters as the meta plugin, so
// installing them as individual plugins for integ tests doesn't make sense,
// so we disable integ tests
integTest.enabled = false

// add all sub-projects of the qa sub-project
gradle.projectsEvaluated {
  project.subprojects
    .find { it.path == project.path + ":qa" }
    .subprojects
    .findAll { it.path.startsWith(project.path + ":qa") }
    .each { check.dependsOn it.check }
}

apply plugin: 'elasticsearch.internal-testclusters'
apply plugin: 'elasticsearch.standalone-rest-test'
apply plugin: 'elasticsearch.rest-test'
apply plugin: 'elasticsearch.rest-resources'

dependencies {
  testImplementation project(':x-pack:qa')
}

restResources {
  restTests {
    includeCore '*'
  }
}

def clusterCredentials = [
        username: providers.systemProperty('tests.rest.cluster.username')
                .orElse('test_admin')
                .forUseAtConfigurationTime()
                .get(),
        password: providers.systemProperty('tests.rest.cluster.password')
                .orElse('x-pack-test-password')
                .forUseAtConfigurationTime()
                .get()
]

tasks.named("integTest").configure {
    systemProperty 'tests.rest.blacklist',
      [
        'index/10_with_id/Index with ID',
        'indices.get_alias/10_basic/Get alias against closed indices'
      ].join(',')

    systemProperty 'tests.rest.cluster.username', clusterCredentials.username
    systemProperty 'tests.rest.cluster.password', clusterCredentials.password
}

testClusters.matching { it.name == "integTest" }.configureEach {
  testDistribution = 'DEFAULT'
  setting 'xpack.security.enabled', 'true'
  setting 'xpack.watcher.enabled', 'false'
  setting 'xpack.ml.enabled', 'false'
  setting 'xpack.license.self_generated.type', 'trial'
  setting 'indices.lifecycle.history_index_enabled', 'false'

  user username: clusterCredentials.username,
    password: clusterCredentials.password
}

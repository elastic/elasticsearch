/*
 * Copyright Elasticsearch B.V. and/or licensed to Elasticsearch B.V. under one
 * or more contributor license agreements. Licensed under the Elastic License
 * 2.0; you may not use this file except in compliance with the Elastic License
 * 2.0.
 */

apply plugin: 'elasticsearch.internal-java-rest-test'
apply plugin: 'elasticsearch.rest-resources'

restResources {
  restApi {
    include '_common', 'bulk', 'field_caps', 'security', 'search', 'clear_scroll', 'scroll', 'async_search', 'cluster',
      'indices', 'open_point_in_time', 'close_point_in_time', 'terms_enum'
  }
}

// Register REST API specs with javaRestTest source set (required for ESClientYamlSuiteTestCase)
sourceSets.javaRestTest.output.dir(
  tasks.named('copyRestApiSpecsTask').flatMap { it.outputResourceDir }
)

// Randomise between sniff and proxy modes
var proxyModeProvider = buildParams.random.map{r -> r.nextBoolean()}

dependencies {
  javaRestTestImplementation project(':x-pack:plugin:core')
  javaRestTestImplementation project(':x-pack:plugin:security')
  clusterModules project(':modules:analysis-common')
  clusterModules project(':modules:lang-painless')
  clusterModules project(':modules:data-streams')
  clusterModules project(':x-pack:plugin:mapper-constant-keyword')
  clusterModules project(':x-pack:plugin:async-search')
}

tasks.named("javaRestTest").configure {
  dependsOn "processJavaRestTestResources"
  maxParallelForks = 1
  usesDefaultDistribution("Multi-cluster search security tests require the default distribution")
  systemProperty 'tests.remote.proxy.mode', proxyModeProvider.get().toString()
}

tasks.named("check").configure { dependsOn("javaRestTest") }

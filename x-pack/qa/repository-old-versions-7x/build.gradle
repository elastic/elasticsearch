import org.elasticsearch.gradle.internal.test.RestIntegTestTask
import org.elasticsearch.gradle.testclusters.StandaloneRestIntegTestTask
import org.elasticsearch.gradle.Version
import org.elasticsearch.gradle.internal.test.rest.CopyRestApiTask
import org.elasticsearch.gradle.internal.test.rest.CopyRestTestsTask
import org.elasticsearch.gradle.internal.test.rest.RestResourcesPlugin

apply plugin: 'elasticsearch.internal-java-rest-test'
apply plugin: 'elasticsearch.rest-resources'

restResources {
  restApi {
    include '_common', 'search'
  }
  restTests {
    includeCore 'search/390_doc_values_search.yml'
  }
}

// Register rest resources with source set
sourceSets.javaRestTest.getOutput()
  .dir(
    project.getTasks()
      .withType(CopyRestApiTask.class)
      .named(RestResourcesPlugin.COPY_REST_API_SPECS_TASK)
      .flatMap(CopyRestApiTask::getOutputResourceDir)
  );

sourceSets.javaRestTest.getOutput()
  .dir(
    project.getTasks()
      .withType(CopyRestTestsTask.class)
      .named(RestResourcesPlugin.COPY_YAML_TESTS_TASK)
      .flatMap(CopyRestTestsTask::getOutputResourceDir)
  );

tasks.named("javaRestTest") {
  enabled = false
}

['7.16.3', '7.17.25'].each { versionString ->
  String versionNoDots = versionString.replace('.', '_')

  tasks.register("javaRestTest#${versionNoDots}", StandaloneRestIntegTestTask) {
    systemProperty 'tests.old_cluster_version', versionString
    usesDefaultDistribution()
    testClassesDirs = sourceSets.javaRestTest.output.classesDirs
    classpath = sourceSets.javaRestTest.runtimeClasspath
    usesBwcDistribution(Version.fromString(versionString))
  }

  tasks.named("check").configure {
    dependsOn "javaRestTest#${versionNoDots}"
  }
}

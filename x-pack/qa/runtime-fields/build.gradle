apply plugin: 'elasticsearch.build'

dependencies {
  api project(":test:framework")
}

// We don't currently have any tests for this because they are test utilities.
tasks.named("test").configure { enabled = false }

subprojects {
  if (project.name.startsWith('core-with-')) {
    apply plugin: 'elasticsearch.yaml-rest-test'

    dependencies {
      yamlRestTestImplementation xpackProject(":x-pack:qa:runtime-fields")
    }

    restResources {
      restApi {
        include '_common', 'bulk', 'count', 'cluster', 'index', 'indices', 'field_caps', 'msearch',
          'search', 'async_search', 'graph', '*_point_in_time'
      }
      restTests {
        includeCore '*'
        includeXpack 'async_search', 'graph'
      }
    }

    testClusters.matching { it.name == "yamlRestTest" }.configureEach {
      testDistribution = 'DEFAULT'
      setting 'xpack.license.self_generated.type', 'trial'
      setting 'xpack.security.enabled', 'false'
    }

    tasks.named("yamlRestTest").configure {
      def suites = [
        'async_search',
        'search',
        'search.aggregation',
        'search.highlight',
        'search.inner_hits',
        'search_shards',
        'suggest',
      ]
      if (project.name.equals('core-with-mapped')) {
        suites += [
          // These two don't support runtime fields on the request. Should they?
          'field_caps',
          'graph',
          // The search request tests don't know how to support msearch for now
          'msearch',
        ]
      }
      systemProperty 'tests.rest.suite', suites.join(',')
      systemProperty 'tests.rest.blacklist',
        [
          /////// TO FIX ///////
          'search.aggregation/40_range/Date range', //source only date field should also emit values for numbers, it expects strings only
          'search/115_multiple_field_collapsing/two levels fields collapsing', // Field collapsing on a runtime field does not work
          'field_caps/30_filter/Field caps with index filter', // We don't support filtering field caps on runtime fields. What should we do?
          'search.aggregation/220_filters_bucket/cache busting', // runtime keyword does not support split_queries_on_whitespace
          'search/140_pre_filter_search_shards/pre_filter_shard_size with shards that have no hit',
          //completion suggester does not return options when the context field is a geo_point runtime field
          'suggest/30_context/Multi contexts should work',

          //there is something wrong when using dotted document syntax here, passes in main yaml tests
          'search/330_fetch_fields/Test nested field inside object structure',
          /////// TO FIX ///////

          /////// NOT SUPPORTED ///////
          'search.highlight/40_keyword_ignore/Plain Highligher should skip highlighting ignored keyword values', // The plain highlighter is incompatible with the prefix queries that we make for runtime fields, use unified highlighter instead.
          'search.aggregation/280_rare_terms/*', // Requires an index and we won't have it
          'search.aggregation/22_terms_disable_opt/*', // Optimization requires an index and runtime fields don't have one
          // Runtime fields don't support sub-fields
          'search.aggregation/10_histogram/*',
          'suggest/50_completion_with_multi_fields/Search by suggestion on geofield-hash on sub field should work',
          // Runtime fields don't have global ords
          'search.aggregation/20_terms/string profiler via global ordinals filters implementation',
          'search.aggregation/20_terms/string profiler via global ordinals native implementation',
          'search.aggregation/20_terms/Global ordinals are loaded with the global_ordinals execution hint',
          'search.aggregation/170_cardinality_metric/profiler string',
          'search.aggregation/235_composite_sorted/*',
          // The error messages are different
          'search/330_fetch_fields/error includes field name',
          'search/330_fetch_fields/error includes glob pattern',
          /////// NOT SUPPORTED ///////
        ].join(',')
    }
  }
}

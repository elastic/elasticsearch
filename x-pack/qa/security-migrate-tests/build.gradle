apply plugin: 'elasticsearch.standalone-rest-test'
apply plugin: 'elasticsearch.rest-test'

dependencies {
  testImplementation project(xpackModule('core'))
  testImplementation project(path: xpackModule('security'))
  testImplementation project(path: xpackProject('transport-client').path)
}

testClusters.matching { it.name == "integTest"}.configureEach {
  testDistribution = "DEFAULT"
  setting 'xpack.security.enabled', 'true'
  setting 'xpack.license.self_generated.type', 'trial'
  extraConfigFile 'roles.yml', file('roles.yml')
  [
    test_admin: 'superuser',
    transport_user: 'superuser',
    existing: 'superuser',
    bob: 'actual_role'
  ].each { String username, String role ->
    user username: username, password: 'x-pack-test-password', role: role
  }
}


tasks.matching { it.name.equals('integTest')}.configureEach {integTestTask ->
  nonInputProperties.systemProperty 'tests.config.dir', "${-> testClusters.integTest.singleNode().getConfigDir()}"
}

tasks.named("testingConventions").configure {
  naming.clear()
  naming {
    IT {
      baseClass 'org.elasticsearch.xpack.security.MigrateToolTestCase'
    }
  }
}

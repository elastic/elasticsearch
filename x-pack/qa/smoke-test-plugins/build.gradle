import org.apache.tools.ant.filters.ReplaceTokens
import org.elasticsearch.gradle.internal.info.BuildParams

apply plugin: 'elasticsearch.internal-testclusters'
apply plugin: 'elasticsearch.standalone-rest-test'
apply plugin: 'elasticsearch.rest-test'
apply plugin: 'elasticsearch.rest-resources'

dependencies {
  testImplementation project(':x-pack:qa')
}

def pluginPaths = project(':plugins').getChildProjects().findAll { pluginName, pluginProject ->
  // Do not attempt to install ingest-attachment in FIPS 140 as it is not supported (it depends on non-FIPS BouncyCastle)
  def ignoreIngestAttachment = BuildParams.inFipsJvm && pluginName == "ingest-attachment"

  // This plugin has to be configured to work via system properties
  def ignoreQuotaWareFs = pluginName == 'quota-aware-fs'

  return ignoreIngestAttachment == false && ignoreQuotaWareFs == false
}.collect {pluginName, pluginProject -> pluginProject.path }

ext.expansions = [
  'expected.plugins.count': pluginPaths.size()
]

tasks.named("processTestResources").configure {
  inputs.properties(project.expansions)
  filter("tokens" : expansions.collectEntries {k, v -> [k, v.toString()]} /* must be a map of strings */, ReplaceTokens.class)
}

testClusters.matching { it.name == "integTest" }.configureEach {
  testDistribution = 'DEFAULT'
  setting 'xpack.security.enabled', 'true'
  setting 'xpack.license.self_generated.type', 'trial'
  user username: "test_user", password: "x-pack-test-password"

  pluginPaths.each {pluginPath ->
    plugin pluginPath
  }
}

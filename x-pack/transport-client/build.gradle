apply plugin: 'elasticsearch.build'
group = 'org.elasticsearch.client'
archivesBaseName = 'x-pack-transport'

dependencies {
  // this "api" dependency looks weird, but it is correct, as it contains
  // all of x-pack for now, and transport client will be going away in the future.
  api project(xpackModule('core'))
  api project(':client:transport')
  testImplementation "com.carrotsearch.randomizedtesting:randomizedtesting-runner:${versions.randomizedrunner}"
  testImplementation "junit:junit:${versions.junit}"
  testImplementation "org.hamcrest:hamcrest:${versions.hamcrest}"
}

tasks.named("dependencyLicenses").configure {
  enabled = false
}

tasks.named("forbiddenApisTest").configure {
  // we don't use the core test-framework, no lucene classes present so we don't want the es-test-signatures to
  // be pulled in
  replaceSignatureFiles 'jdk-signatures', 'es-all-signatures'
}

tasks.named('testTestingConventions').configure {
  baseClass 'com.carrotsearch.randomizedtesting.RandomizedTest'
}